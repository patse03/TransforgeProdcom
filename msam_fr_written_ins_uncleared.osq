/*-s-------------------------------------------------------------------------
		       msam_fr_written_ins_uncleared
 Selects all uncleared written ins/uncoded entries by user_id and period.
--------------------------------------------------------------------------e-*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_inquiry         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        h_field_name      = VARCHAR(24) NOT NULL,
        h_rows            = INTEGER4 NOT NULL,
        ret_val           = INTEGER1 NOT NULL
{
    h_retries_allowed = 3;
    h_frpr_name = 'msam_fr_written_ins_uncleared';

    MESSAGE 'Selecting Uncleared Written ins . . .';

    SET_FORMS FIELD '' (MODE(tf) = 'READ',
                        DISPLAYONLY(period) = 0,
                        REVERSE(period) = 0,
                        UNDERLINE(period) = 1);

    RESUME FIELD period;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');
}

FIELD period =
{
    MESSAGE 'Checking Period . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT inquiry AS h_inquiry
        FROM   inquiry_period
        WHERE  period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing inquiry_period'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Period does not exist'
        WITH STYLE = POPUP;
        RESUME FIELD period;
    ENDIF;

    SET_FORMS FIELD '' (DISPLAYONLY(period) = 1,
                        REVERSE(period) = 1,
                        UNDERLINE(period) = 0);

    MESSAGE 'Selecting Data . . .';

    IF :g_user_id = 'proddc11' OR :g_user_id = 'proddc10'
    THEN

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            tf =

            SELECT   a.contributor_reference,
		     a.question,
		     a.contributor_industry,
		     a.avalue,
		     b.receipt_date
            FROM     cqpv a,
		     contributor_period b
            WHERE    a.period = :period
	    AND      a.acell = 'W'
	    AND      a.contributor_reference = b.contributor_reference
	    AND      a.period = b.period
            ORDER BY contributor_industry,contributor_reference;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;

        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in selecting from cqpv'
            WITH STYLE = POPUP;
            RETURN 9;

        ELSEIF ret_val = 1
        THEN
            MESSAGE 'No rows found on cqpv'
            WITH STYLE = POPUP;
            RETURN 9;

            SET_FORMS FIELD '' (DISPLAYONLY(period) = 0,
                                REVERSE(period) = 0,
                                UNDERLINE(period) = 1);
                        
            RESUME FIELD period;
        ENDIF;
    ELSE

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            tf =

            SELECT   a.contributor_reference,
		     a.question,
		     a.contributor_industry,
		     a.avalue,
		     b.receipt_date
            FROM     cqpv a,
		     contributor_period b
            WHERE    a.period = :period
	    AND      a.acell = 'W'
	    AND      a.contributor_reference = b.contributor_reference
	    AND      a.period = b.period
	    AND      a.contributor_industry in (
		     SELECT industry 
		     FROM  industry_staff 
		     WHERE user_id = :g_user_id)
            ORDER BY contributor_industry,contributor_reference;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;

        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in selecting from cqpv'
            WITH STYLE = POPUP;
            RETURN 9;

        ELSEIF ret_val = 1
        THEN
            MESSAGE 'No rows found on cqpv'
            WITH STYLE = POPUP;
            RETURN 9;

            SET_FORMS FIELD '' (DISPLAYONLY(period) = 0,
                                REVERSE(period) = 0,
                                UNDERLINE(period) = 1);
                        
            RESUME FIELD period;
        ENDIF;
    ENDIF;
}

'Clear' =
{
    CLEAR FIELD ALL;
    msa_date = 'now';

    SET_FORMS FIELD '' (DISPLAYONLY(period) = 0,
                        REVERSE(period) = 0,
                        UNDERLINE(period) = 1);
                        
    RESUME FIELD period;
}

'ContactDetails' =
{
    INQUIRE_FORMS FORM (h_field_name = FIELD);

    IF :h_field_name <> 'tf'
    THEN
        MESSAGE 'Please highlight the required Contributor'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    INQUIRE_FORMS TABLE '' (h_rows = DATAROWS(tf));

    IF :h_rows = 0
    THEN
        MESSAGE 'Please highlight the required Contributor'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    CALL APPLICATION ( exec = '$IMAGES/communicate -numeric_overflow=ignore',
                       param = VARCHAR(:tf.contributor_reference));
}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}

