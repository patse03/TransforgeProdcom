/*----------------------------------------------------------------------
msaj_fr_choose_reason
----------------------------------------------------------------------*/

INITIALIZE ( contact_message    = CHAR(50) NOT NULL) =

DECLARE      h_frpr_name        = VARCHAR(25) NOT NULL,
             h_retries_allowed  = INTEGER1 NOT NULL,
             h_retries          = INTEGER1 NOT NULL,
             ret_val            = INTEGER1 NOT NULL,
             tf.message_no      = INTEGER4 NOT NULL,
             h_retry_or_not     = CHAR(5)  NOT NULL
{
    h_frpr_name = 'msaj_fr_choose_reason';
    h_retries_allowed = 3;

    SET_FORMS FIELD '' (MODE(tf) = 'READ');

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE (:h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY')
    DO
        h_retries = :h_retries + 1;

        tf =

        SELECT   message_no,
                 response_message
        FROM     contact_response_codes
        ORDER BY message_no;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in selecting from contact_response_codes'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows loaded from contact_response_codes'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'Help',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');
}

'SelectMessage', KEY FRSKEY11 =
{
    contact_message = :tf.response_message;
    RETURN 0;
}

'End', KEY FRSKEY14  =
{
    RETURN 1;
}
