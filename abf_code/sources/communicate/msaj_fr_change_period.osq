/*----------------------------------------------------------------------
msaj_fr_change_period
----------------------------------------------------------------------*/

INITIALIZE (

        inquiry           = INTEGER1 NOT NULL) =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL

{
    h_frpr_name = 'msaj_fr_change_period';
    h_retries_allowed = 3;
}

FIELD period =
{

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT period
        FROM   inquiry_period
        WHERE  period = :period
        AND    inquiry = :inquiry;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing inquiry_period'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This is not a valid Period for this Contributor''s' +
                ' Inquiry'
        WITH STYLE = POPUP;
        RESUME FIELD period;
    ENDIF;

    RETURN 0;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'Help',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');
}

'EndWithoutChanging', KEY FRSKEY6  =
{
    RETURN 1;
}
