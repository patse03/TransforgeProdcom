/***************************************************************************
                          msam_fr_main_menu

  This frame displays the main Management Information options.

***************************************************************************/

INITIALIZE () =

DECLARE 
  h_return           = INTEGER2    NOT NULL WITH DEFAULT,
  h_return2          = INTEGER2    NOT NULL WITH DEFAULT,
  h_frpr_name        = VARCHAR(24) NOT NULL WITH DEFAULT,
  h_rap_option       = CHAR(1)     NOT NULL WITH DEFAULT,
  h_rr_option        = CHAR(1)     NOT NULL WITH DEFAULT,

{
  msa_date = 'now';
  h_frpr_name  = 'msam_fr_main_menu';
  h_rap_option = 'N';
  h_rr_option  = 'N';

  SET_FORMS FRS (MAPFILE='/home/prodcom/admin/prodcom.map');

  SET_FORMS FIELD '' (INVISIBLE(s_rap_option)  = 1);

  s_rap_option = 'RAP Team Information ............... 4';
  s_rr_option = 'REV Team Information ............... 5';

  /*---------------------------------------------------------------------
       Set global variables.
  ---------------------------------------------------------------------*/

  SELECT DBMSINFO('username') AS g_user_id;
  SELECT DBMSINFO('database') AS g_database;

  g_help = callproc msa_cp_getvar('PROD_HELP');

  IF :g_database = 'prodlivedb'
  THEN
    g_path_name1 = '/prodcom/live5/files/sas';
  ELSE
    g_path_name1 = '/prodcom/source/produser/files/sas';
  ENDIF;

  h_return = CALLPROC msa_fp_get_user_details 
           ( p_default_printer = BYREF(:g_printer),
             p_user_name       = BYREF(:g_user_name));

  IF h_return != 0
  THEN

    /* Error message handled in external procedure */

    IF h_return > 7
    THEN
      h_return2 = CALLPROC msa_fp_update_iip (h_exit = 'Y');
    ENDIF;

    RETURN;

  ENDIF;

  /*----------------------------------------------------------------------
       Access check. (Error messages handled by procedure.)

       NB: This is required not for access to menu, but to the globals 
           for user level and user class so that they can be used
           to set menu options.
  ----------------------------------------------------------------------*/
  
  h_return = CALLPROC check_access_frames (p_frpr_name = :h_frpr_name);

  IF h_return = 1 
  THEN
    RETURN;
  ELSEIF h_return > 1
  THEN
    h_return = CALLPROC msa_fp_update_iip (h_exit = 'Y');
    RETURN;
  ENDIF;
  
  /*----------------------------------------------------------------------
       Display RAP menu option if user has R or D access class.
  ----------------------------------------------------------------------*/

  IF g_user_class = 'R'
  OR g_user_class = 'D'
  OR g_user_level = 5
  THEN
    h_rap_option = 'Y';
    SET_FORMS FIELD '' (INVISIBLE(s_rap_option)  = 0);
  ENDIF;
  
  /*----------------------------------------------------------------------
       Display RR menu option if user has O or D access class.
  ----------------------------------------------------------------------*/

  IF g_user_class = 'P'
  OR g_user_class = 'D'
  OR g_user_level = 5
  THEN
    h_rr_option = 'Y';
    SET_FORMS FIELD '' (INVISIBLE(s_rr_option)  = 0);
  ENDIF;
}

/*************************** FIELD ACTIVATIONS ****************************/

FIELD s_choice =

{
    msa_date = 'now';
    h_return = 0;

    IF s_choice = 1
    THEN
      h_return = CALLFRAME msam_fr_status_menu();

    ELSEIF s_choice = 2
    THEN
      h_return = CALLFRAME msam_fr_progress_menu();

    ELSEIF s_choice = 3
    THEN
      h_return = CALLFRAME msam_fr_reference_menu();

    ELSEIF s_choice = 4
    AND    h_rap_option = 'Y'
    THEN
      h_return = CALLFRAME msam_fr_rap_menu();

    ELSEIF s_choice = 5
    AND    h_rr_option = 'Y'
    THEN
      h_return = CALLFRAME msam_fr_rr_menu();

    ELSEIF s_choice = 0
    THEN
      RETURN;

    ELSE
      MESSAGE 'WARNING: Please select a valid option from the menu.' 
      WITH STYLE = POPUP;
      msa_date = 'now';
      RESUME FIELD s_choice;
   ENDIF;
 
  /*---------------------------------------------------------------------
        Deal with returns
  ---------------------------------------------------------------------*/

  IF h_return > 7
  THEN
    h_return = CALLPROC msa_fp_update_iip (h_exit = 'Y');
    RETURN;
  ENDIF;

  RESUME FIELD s_choice;
}

