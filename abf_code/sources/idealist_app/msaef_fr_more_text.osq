/*-----------------------------------------------------------------

                     msaef_fr_more_text

  This frame displays all lines of text for a certain prodcom
  code.

  Called by:         msaef_fr_top_frame
------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        h_question        = INTEGER4 NOT NULL,
        h_unit_1          = CHAR(20) NOT NULL,
        ret_val           = INTEGER1 NOT NULL

{
    h_retries_allowed = 3;
    msa_date = 'now';
    h_frpr_name = 'msaef_fr_more_text';

    vol02_text = '';
    vol04_text = '';
    volume_02 = 'N';
    volume_04 = 'N';
    volume_05 = 'N';
    volume_06 = 'N';
    volume_08 = 'N';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT text_line1 AS line1,
               text_line2 AS line2,
               text_line3 AS line3,
               text_line4 AS line4,
               text_line5 AS line5,
               text_line6 AS line6,
               text_line7 AS line7,
               text_line8 AS line8,
               text_line9 AS line9,
               text_line10 AS line10
        FROM   question_library
        WHERE  question = :pcc_code
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Question ' + VARCHAR(:pcc_code) +
                ' does not exist for ' + VARCHAR(:period)
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Load Volumes
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question AS h_question,
               unit_1 AS h_unit_1
        FROM   question_library
        WHERE  question IN (:pcc_code, :pcc_code + 1, :pcc_code + 3,
                            :pcc_code + 5, :pcc_code + 7)
        AND    period = :period
        {
            IF MOD(:h_question,10) = 2
            THEN
                volume_02 = 'Y';
                vol02_text = :h_unit_1;
            ELSEIF MOD(:h_question,10) = 4
            THEN
                volume_04 = 'Y';
                vol04_text = :h_unit_1;
            ELSEIF MOD(:h_question,10) = 5
            THEN
                volume_05 = 'Y';
            ELSEIF MOD(:h_question,10) = 6
            THEN
                volume_06 = 'Y';
            ELSEIF MOD(:h_question,10) = 8
            THEN
                volume_08 = 'Y';
            ENDIF;

        };

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY14 =
{
    RETURN 0;
}
