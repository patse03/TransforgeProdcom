/*----------------------------------------------------------------------
NOTE - FOR PQL, REVERT TO THE 2 COMMENTED OUT BLOCKS OF CODE ACCESSING
QUESTION_LIBRARY

Displays full screen of question text
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL,
        search_question   = INTEGER4 NOT NULL

{
    h_frpr_name = 'msaeb_fr_desp_question';
    h_retries_allowed = 3;
    msa_date = 'now';
    SET_FORMS FIELD '' (INVISIBLE(units) = 1);

    period = :period / 100 * 100;

    /*------------------------------------------------------------------
     Loads question, if it's there
    ------------------------------------------------------------------*/

    IF VARCHAR(:question) LIKE '%2' OR VARCHAR(:question) LIKE '%4'
    OR VARCHAR(:question) LIKE '%5' OR VARCHAR(:question) LIKE '%6'
    OR VARCHAR(:question) LIKE '%7' OR VARCHAR(:question) LIKE '%8'
    THEN
        search_question := (:question / 10 * 10) + 1;
    ELSE
        search_question := :question;
    ENDIF;

    h_retry_or_not = 'RETRY';
    h_retries = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        SELECT unit_1 AS units,
               text_line1 AS line1,
               text_line2 AS line2,
               text_line3 AS line3,
               text_line4 AS line4,
               text_line5 AS line5,
               text_line6 AS line6,
               text_line7 AS line7,
               text_line8 AS line8,
               text_line9 AS line9,
               text_line10 AS line10
        FROM   question_library
        WHERE  question = :search_question
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question table'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Question ' + VARCHAR(:search_question) + 
                ' was not found for the annual period ' +
                VARCHAR(:period)
        WITH STYLE = POPUP;
        RETURN 1;          
    ENDIF;

    IF :question <> :search_question
    THEN
        h_retry_or_not = 'RETRY';
        h_retries = 0;
    
        WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO
            h_retries = :h_retries + 1;
    
            SELECT unit_1 AS units
            FROM   question_library
            WHERE  question = :question
            AND    period = :period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;

        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in accessing question table'
            WITH STYLE = POPUP;
            RETURN 9;

        ELSEIF ret_val = 1
        THEN
            MESSAGE 'Question ' + VARCHAR(:question) + 
                    ' was not found for the annual period ' +
                    VARCHAR(:period)
            WITH STYLE = POPUP;
            RETURN 1;          
        ENDIF;

    ENDIF;

    IF units = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(units) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(units) = 0);
    ENDIF;

    REDISPLAY;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
