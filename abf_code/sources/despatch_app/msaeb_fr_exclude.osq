/*----------------------------------------------------------------------
Allows various options to be included/excluded from a despatch list,
and produces a where-clause to accomplish this.

The 4 include_xx variables are passed into this frame by the calling
frame msaeb_fr_despatch_nr (so that any changes made by this program
will be present the next time this program is called).
----------------------------------------------------------------------*/

INITIALIZE (

        include_cbu       = VARCHAR(7) NOT NULL,
        include_ni        = VARCHAR(7) NOT NULL,
        include_enf       = VARCHAR(7) NOT NULL,
        include_0_9       = VARCHAR(7) NOT NULL,
        where_clause      = VARCHAR(500) NOT NULL) =

DECLARE h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL
{
    h_frpr_name = 'msaeb_fr_exclude';

    /*------------------------------------------------------------------
    Insert the categories that may be excluded.
    If they are to be excluded, put "Exclude" (otherwise leave blank).
    ------------------------------------------------------------------*/

    INSERTROW tf [0] (text = 'CBU', inclexcl = :include_cbu);
    INSERTROW tf [1] (text = 'Northern Ireland', inclexcl = :include_ni);
    INSERTROW tf [2] (text = 'Enforcement', inclexcl = :include_enf);
    INSERTROW tf [3] (text = '0-9', inclexcl = :include_0_9);

    SET_FORMS FIELD '' (MODE(tf) = 'READ');
    SCROLL tf TO 1;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'SelectCategory' =
{
    IF tf.inclexcl = 'Exclude'
    THEN
        tf.inclexcl = '';
    ELSE
        tf.inclexcl = 'Exclude';
    ENDIF;
}

'Go', KEY FRSKEY4 =
{
    /*------------------------------------------------------------------
    Put the new values in the 4 include_xx variables.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF tf.text = 'CBU'
        THEN
            include_cbu = tf.inclexcl;
        ELSEIF tf.text = 'Northern Ireland'
        THEN
            include_ni = tf.inclexcl;
        ELSEIF tf.text = 'Enforcement'
        THEN
            include_enf = tf.inclexcl;
        ELSEIF tf.text = '0-9'
        THEN
            include_0_9 = tf.inclexcl;
        ENDIF;
    };

    /*------------------------------------------------------------------
    Rebuild the where-clause based on the contents of the 4 include_xx
    variables.
    ------------------------------------------------------------------*/

    where_clause = '';

    IF include_cbu = 'Exclude'
    THEN
        where_clause = where_clause +
                       'and output_category <> ''Z'' ';
    ENDIF;

    IF include_ni = 'Exclude'
    THEN
        where_clause = where_clause +
                       'and region <> ''YY'' ';
    ENDIF;

    IF include_enf = 'Exclude'
    THEN
        where_clause = where_clause +
                       'and output_category <> ''Y'' ';
    ENDIF;

    IF include_0_9 = 'Exclude'
    THEN
        where_clause = where_clause +
                       'and register_emp not between 0 and 9 ';
    ENDIF;

    RETURN 0;
}

'EndWithoutChanging', KEY FRSKEY6 =
{
    RETURN 1;
}
