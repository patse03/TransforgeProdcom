/*--------------------------------------------------------------------

              msafd_rw_r048_newlu - NEW LOCAL UNIT

Called by msafd_fr_cr_luadd1 / msafd_fr_cra_luadd1

This report allows the production of the R048 form, which is completed
and issued to registers each time a contributor's new local unit
is selected and periodic data created. 

Tables used:     contributor_luaddress 
                 contributor_period
                 contributor

-------------------------------------------------------------------*/

.name                  msafd_rw_r048_newlu
.output                msafd_rw_r048_newlu.rpt

.declare               print_name = VARCHAR(50),
                       divider = VARCHAR(3) WITH VALUE ' / '

.query          SELECT a.contributor_reference,
                       a.lu_reference,
                       a.period,
                       a.register_emp,
                       a.lu_name1,
                       c.add_name1
                FROM   contributor_luaddress a,
                       contributor c
                WHERE  '$contributor_reference' = a.contributor_reference
                AND    '$lu_reference' = a.lu_reference
                AND    a.contributor_reference = c.contributor_reference
                AND    $period = a.period

.format                contributor_reference    (c11)
                       lu_reference             (c11)
                       add_name1                (c35)
                       lu_name1                 (c35)
                       period                   (-f6)
                       register_emp             (-f5)

.pagelength 85

.detail                
                       .let print_name = TRIM(add_name1) + divider + lu_name1

                       .pr '1'
                       .nl 8
                       .tab 70 .pr $name(c24)
                       .nl
/* .tab 08 .pr ' MSI   PQI   PAI   PSI   CS   QCI  ACOP  EMI  ACOC' */
                       .nl
/* .tab 08 .pr '  08    15    14    17   17    19   01    24   11 ' */
                       .tab 76 .pr $inquiry(f2)
                       .nl 2
                       .tab 70 .pr $room_number(c6)
                       .nl 2
                       .tab 70 .pr current_date(d"03/02/1901")
                       .tab 89 .pr $extension(c9)
                       .nl 3
                       .tab 25 .pr print_name
                       .nl 2
                       .tab 25 .pr contributor_reference ' / ' lu_reference
                       .tab 95 .pr $contributor_industry(f5)
                       .nl 4
                       .tab 25 .pr 'PLEASE ADD LOCAL UNIT DETAILS FOR'
                                  ' PERIOD ' period
                       .nl 13 

                       .tab 34 .pr 'NEW LOCAL UNIT' .nl
                       .tab 34 .pr '--------------' .nl 2

                       .tab 34 .pr 'Inquiry = ' $inquiry (f2)
                       .nl 2
                       .tab 34 .pr 'Period  = ' period
                       .nl 2
                       .tab 34 .pr 'Reg Emp = ' register_emp
                       .nl 3
                       .tab 34 .pr 'Selected on ' 
                                   current_date(d"3rd February 1901")
                       .nl 23 

                       .tab 14 .pr $name(c24)
                       .nl 3
                       .tab 20 .pr $inquiry(f2)
                       .nl 2
                       .tab 14 .pr $room_number(c6)
                       .nl 2
                       .tab 18 .pr print_name 
                       .nl 3
                       .tab 33 .pr contributor_reference ' / ' lu_reference
                       .tab 95 .pr $contributor_industry(f5)

                       .np
                       .nl
