/*----------------------------------------------------------------------
  Updates the internal password 
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_user_id_2       = CHAR(10) NOT NULL,
        h_comp_pass       = CHAR(5)  NOT NULL,
        h_attempts        = INTEGER1 NOT NULL,
        h_frpr_name       = VARCHAR(25) NOT NULL,
        ret_val           = INTEGER1 NOT NULL,
        h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5)  NOT NULL

{
    msa_date = 'now';
    h_attempts = 0;
    h_frpr_name = 'msap_fr_up_password';
    h_retries_allowed = 3;
}

/*************************** FIELD ACTIVATIONS ****************************/

FIELD old_password = 
{
    msa_date = 'now';
    old_password = SQUEEZE(:old_password);

    IF LENGTH(:old_password) <> 5
    THEN
        MESSAGE 'Your password must be 5 characters in length.' +
                ' Please try again.'
        WITH STYLE = POPUP;
        CLEAR FIELD old_password;
        RESUME FIELD old_password;
    ENDIF;
    
    IF old_password <> :g_password
    THEN
        MESSAGE 'You have not typed your existing password correctly.' +
                ' Please try again.'
        WITH STYLE = POPUP;
        CLEAR FIELD old_password;
        RESUME FIELD old_password;
    ENDIF;
    
    RESUME FIELD new_password;
}

FIELD 'new_password' =
{
    msa_date = 'now';
    new_password = SQUEEZE(:new_password);

    h_attempts = h_attempts + 1;

    IF LENGTH(:new_password) <> 5
    THEN
        MESSAGE 'Your password must be 5 characters in length'
        WITH STYLE = POPUP;

        IF h_attempts = 3
        THEN
            MESSAGE 'Three attempts have been made. The system will now exit.'
            WITH STYLE = POPUP;
            RETURN 9;
        ENDIF;

        CLEAR FIELD new_password;
        RESUME FIELD new_password;
    ENDIF;

    IF new_password = :g_password
    THEN
        MESSAGE 'You cannot use the same password'
        WITH STYLE = POPUP;

        IF h_attempts = 3
        THEN
            MESSAGE 'Three attempts have been made. The system will now exit.'
            WITH STYLE = POPUP;
            RETURN 9;
        ENDIF;

        CLEAR FIELD new_password;
        RESUME FIELD new_password;
    ENDIF;

    h_comp_pass = new_password;

    ret_val = CALLFRAME msap_fr_ck_password (
               h_comp_pass = BYREF(:new_password));

    msa_date = 'now';
    IF ret_val <> 0
    THEN
        MESSAGE 'Error calling second password check'
        WITH STYLE = POPUP;
        RETURN 9
    ENDIF;

    IF h_comp_pass <> :new_password
    THEN
         MESSAGE 'Confirmation of the new password has failed.' +
                 ' Please type in your new password again.'
         WITH STYLE = POPUP;
         CLEAR FIELD new_password;
         RESUME FIELD new_password;
         msa_date = 'now';
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE passwords_new
        SET    password     = :new_password,
               date_changed = 'today'
        WHERE  user_id      = :g_user_id;

        ret_val = CALLPROC check_inq_ing (
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_rollback        = 'N',
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in changing password'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows updated while changing password'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE passwords
        SET    password     = :new_password,
               date_changed = 'today'
        WHERE  user_id      = :g_user_id;

        ret_val = CALLPROC check_inq_ing (
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_rollback        = 'N',
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in changing password'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows updated while changing password on old table.'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    g_password = :new_password;
    MESSAGE 'New password accepted OK'
    WITH STYLE = POPUP;

    CLEAR FIELD ALL;

    RETURN 0;
}

/**************************** KEY ACTIVATIONS *****************************/

'End', KEY FRSKEY14 = 
{
    RETURN 0; 
}
