/*------------------------------------------------------------------
 Allows control to delete/add industries to staff
------------------------------------------------------------------*/

INITIALIZE (
        h_job_title        = CHAR(24) NOT NULL) =

DECLARE h_yesno            = CHAR(1) NOT NULL,
        h_frpr_name        = VARCHAR(25) NOT NULL,
        lp_load_industries = PROCEDURE RETURNING INTEGER,
        ret_val            = INTEGER1 NOT NULL,
        h_retries_allowed  = INTEGER1 NOT NULL,
        h_retries          = INTEGER1 NOT NULL,
        h_retry_or_not     = CHAR(5)  NOT NULL

{
    h_frpr_name = 'msap_fr_up_industries';
    h_retries_allowed = 3;
    msa_date = 'now';

    SET_FORMS FORM (MODE = 'read');

    ret_val = CALLPROC lp_load_industries ();

    IF ret_val <> 0
    THEN
        MESSAGE 'Error in calling load_industries procedure'
        WITH STYLE = POPUP;
        RETURN ret_val;
    ENDIF;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'help',
               FILE    = :g_help + '/' + :h_frpr_name + '.hlp');
    msa_date = 'now';
}

'Delete'  =
{
    msa_date = 'now';
    IF :count_ind = 0
    THEN
        MESSAGE 'No industries to delete'
        WITH STYLE = POPUP;
        RESUME FIELD tf;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        DELETE FROM industry_staff
        WHERE       industry  = :tf.industry
        AND         user_id   = :user_id;

        ret_val = CALLPROC check_inq_ing (
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_rollback        = 'N',
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in deleting from industry_staff'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows deleted from industry_staff'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    ret_val = CALLPROC lp_load_industries ();

    IF ret_val <> 0
    THEN
        MESSAGE 'Error in calling load_industries procedure'
        WITH STYLE = POPUP;
        RETURN ret_val;
    ENDIF;
}

'DeleteAll' =
{
    msa_date = 'now';
    IF :count_ind = 0
    THEN
        MESSAGE 'No industries to delete'
        WITH STYLE = POPUP;
        RESUME FIELD tf;
    ENDIF;

    h_yesno = '';
    WHILE h_yesno <> 'y' AND h_yesno <> 'n'
    DO
        h_yesno = PROMPT 'Really delete all industries? Type Y/N and' +
                         ' press <return>'
                  WITH STYLE = POPUP;
        h_yesno = LOWERCASE(:h_yesno);
    ENDWHILE;

    IF h_yesno = 'n'
    THEN
        RESUME;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        DELETE FROM industry_staff
        WHERE       user_id   = :user_id;

        ret_val = CALLPROC check_inq_ing (
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_rollback        = 'N',
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in deleting all from industry_staff'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows deleted (all) from industry_staff'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    ret_val = CALLPROC lp_load_industries ();

    IF ret_val <> 0
    THEN
        MESSAGE 'Error in calling load_industries procedure'
        WITH STYLE = POPUP;
        RETURN ret_val;
    ENDIF;
}

'Insert' =
{
    ret_val = CALLFRAME msap_fr_in_industries (
              user_id   = :user_id,
              user_name = :user_name,
              h_job_title = :h_job_title);

    IF ret_val <> 0
    THEN
        MESSAGE 'Error in procedure to insert new industries'
        WITH STYLE = POPUP;
        RETURN ret_val;
    ENDIF;
    msa_date = 'now';

    ret_val = CALLPROC lp_load_industries ();

    IF ret_val <> 0
    THEN
        MESSAGE 'Error in calling load_industries procedure'
        WITH STYLE = POPUP;
        RETURN ret_val;
    ENDIF;
}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}

/*----------------------------------------------------------------------
                L O C A L   P R O C E D U R E S
----------------------------------------------------------------------*/

PROCEDURE lp_load_industries () =
{

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        tf =

        SELECT a.industry,
               b.industry_text
        FROM   industry_staff a,
               industry b
        WHERE  a.industry = b.industry
        AND    a.user_id = :user_id;

        ret_val = CALLPROC check_inq_ing (
                   h_num_row         = BYREF(count_ind),
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_frpr_name       = :h_frpr_name,
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 9;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in loading industry details'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        count_ind = 0;
    ENDIF;

    RETURN 0;
}
