/*------------------------------------------------------------------
 Allows control to pick a required access level
------------------------------------------------------------------*/

INITIALIZE (

        h_access_level    = INTEGER4 NOT NULL) =

DECLARE h_frpr_name       = VARCHAR(25) NOT NULL,
        h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        ret_val           = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5)  NOT NULL

{
    h_frpr_name = 'msap_fr_select_al';
    h_retries_allowed = 3;
    SET_FORMS FORM (MODE = 'read'); 

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        tf = 

        SELECT     access_level,
                   short_description
        FROM       access_levels
        ORDER BY   access_level DESC; 

        ret_val = CALLPROC check_inq_ing (
                   h_num_row_ck      = 1,
                   h_commit          = 'Y',
                   h_rollback        = 'N',
                   h_retries         = :h_retries,
                   h_retries_allowed = :h_retries_allowed,
                   h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in loading table with access_levels'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'help',
               FILE    = :g_help + '/' + :h_frpr_name + '.hlp');
}

'Select', KEY FRSKEY4 =
{
    h_access_level = :tf.access_level;
    RETURN 0;
}

'EndWithoutSelecting', KEY FRSKEY6 =
{
    RETURN 0;
}
