/*----------------------------------------------------------------------
                       msau_fr_select

    This frame is the only frame within the QuickView
    application, and is used to select which application is
    to be "QuickViewed".
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed          = INTEGER1 NOT NULL,
        h_retries                  = INTEGER1 NOT NULL,
        h_retry_or_not             = CHAR(5) NOT NULL,
        h_frpr_name                = VARCHAR(24) NOT NULL,
        h_return                    = INTEGER1 NOT NULL,
        access_level               = INTEGER1 NOT NULL,
        tf.h_application_name      = VARCHAR(50) NOT NULL,
        h_user_name                = VARCHAR(20) NOT NULL,
        h_english_name             = VARCHAR(35) NOT NULL,
        h_param1                   = VARCHAR(10) NOT NULL,
        h_varchar_variable         = VARCHAR(100) NOT NULL,
        h_integer_variable         = INTEGER1 NOT NULL,
        h_param_list               = VARCHAR(100) NOT NULL,
        h_date                     = DATE NOT NULL
{
    SELECT DBMSINFO('username') AS g_user_id;
    SELECT DBMSINFO('database') AS g_database;

    SET_FORMS FRS (MAPFILE='/home/prodcom/admin/prodcom.map');

    SET_FORMS FIELD '' (MODE(tf) = 'READ');

    h_retries_allowed   = 3;
    h_frpr_name         = 'msau_fr_select';
    msa_date = 'now';
    h_param_list = CALLPROC CommandLineParameters();

    h_varchar_variable = squeeze (h_param_list);

    h_integer_variable = locate (h_varchar_variable, ' ');
    h_param1         =   left (h_varchar_variable,h_integer_variable - 1);
    SELECT DBMSINFO('username') AS h_user_name;
    SELECT user_name AS g_user_id FROM iidbconstants;


  /*----------------------------------------------------------------------
      This is an often used function so a good time to check for
      system messages being broadcast.
  ----------------------------------------------------------------------*/

  CALL APPLICATION 
  (EXEC = '$IMAGES/first_application -numeric_overflow=ignore '
        + 'check_motd');

  /*----------------------------------------------------------------------
       Access check. (Error messages handled by procedure.)
  ----------------------------------------------------------------------*/
  
  h_return = CALLPROC check_access_frames (p_frpr_name = :h_frpr_name);

  IF h_return = 1 
  THEN
    RETURN 0;
  ELSEIF h_return > 1
  THEN
    RETURN 9;
  ENDIF;

    /*------------------------------------------------------------------
       See if the user_id has already switched once before this
       session - if so, don't allow another switch
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT    english_name AS h_english_name,
                  time         AS h_date
        FROM      quickview_history
        WHERE     user_name = :h_user_name;

        h_return = CALLPROC check_inq_ing (
                  h_num_row_ck      = 0,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF h_return != 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF h_return = 7
    THEN
        RETURN 7;

    ELSEIF h_return = 9
    THEN
        MESSAGE 'System error in accessing quickview_history'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF h_return = 1
    THEN
        MESSAGE SQUEEZE('Sorry, but you have already used ' +
                 'QuickView to switch to "' +
                :h_english_name + '" at ' + VARCHAR(:h_date) +
                ' - you need to quit that before you can switch again.')
        WITH STYLE=POPUP;
        RETURN 1;
    ENDIF;



    /*------------------------------------------------------------------
      Load the form table with switching options
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        tf = 

        SELECT    english_name,
                  application_name AS h_application_name
        FROM      quickview_applications
        ORDER BY  english_name;

        h_return = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF h_return != 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF h_return = 7
    THEN
        RETURN 7;

    ELSEIF h_return = 9
    THEN
        MESSAGE 'System error in loading from quickview_applications'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF h_return = 1
    THEN
        MESSAGE 'No rows loaded from quickview_applications'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = '$PROD_HELP/' + :h_frpr_name + '.hlp');

}

'Go', KEY FRSKEY11 =
{
    /*------------------------------------------------------------------
       Check if user trying to access takeon from takeon
       and prevent .
    ------------------------------------------------------------------*/

    IF left(:tf.english_name,4) = left(:h_param1,4)
    THEN
        MESSAGE SQUEEZE('Sorry, but you have already in take-on' +
                ' - you need to choose another quickview option.')
        WITH STYLE=POPUP;
        RETURN 1;
    ENDIF;

    /*------------------------------------------------------------------
     Insert note in history table
    ------------------------------------------------------------------*/

    msa_date = 'now';
    MESSAGE :tf.english_name + ' Selected . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        INSERT INTO quickview_history (
                    user_name,
                    english_name,
                    time)
        VALUES (    :h_user_name,
                    :tf.english_name,
                    'now');

        h_return = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF h_return != 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF h_return = 7
    THEN
        RETURN 7;

    ELSEIF h_return = 9
    THEN
        MESSAGE 'System error in inserting row into quickview'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF h_return = 1
    THEN
        MESSAGE 'No row inserted into quickview'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
       Switch to new application 
    ------------------------------------------------------------------*/

    MESSAGE 'About to call ' + :tf.english_name + ' . . .';

    IF :tf.h_application_name = 'communicate'
    THEN
        CALL APPLICATION(exec = '$IMAGES/communicate',param='MENU');
    ELSE
        CALL APPLICATION(exec = '$IMAGES/'+ :tf.h_application_name 
					  + ' -numeric_overflow=ignore '
                                           );
    ENDIF;

    MESSAGE 'Returned from ' + :tf.english_name + ' . . .';

    /*------------------------------------------------------------------
       Remove note from history table
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        DELETE FROM    quickview_history
        WHERE          user_name = :h_user_name;

        h_return = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF h_return != 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF h_return = 7
    THEN
        RETURN 7;

    ELSEIF h_return = 9
    THEN
        MESSAGE 'System error in deleting from quickview_history'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF h_return = 1
    THEN
        MESSAGE 'No rows deleted from quickview_history'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    msa_date = 'now';
}

'End', KEY FRSKEY14 =
{
    RETURN 0;
}
