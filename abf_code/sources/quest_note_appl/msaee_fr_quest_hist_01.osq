/*----------------------------------------------------------------------
This shows the history of a question as in the communicate application:
    Frame 01 - accepts the question
    Frame 02 - shows the history
    Frame 03 - adds new details
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL

{
    msa_date = 'now';
    h_retries_allowed = 3;
    h_frpr_name = 'msaee_fr_quest_history';
}

FIELD question =
{
    /*------------------------------------------------------------------
     Check Question exists on the question_library
    ------------------------------------------------------------------*/

    MESSAGE 'Checking Question . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question 
        FROM   question_library
        WHERE  question = :question;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This Question does not exist on the Question Library'
        WITH STYLE = POPUP;
        RESUME FIELD question;
    ENDIF;

    /*------------------------------------------------------------------
     Call 2nd frame to display the history of the question
    ------------------------------------------------------------------*/

    ret_val = CALLFRAME msaee_fr_quest_hist_02 (
              question = :question);

    CLEAR FIELD ALL;
    msa_date = 'now';
    RESUME FIELD question;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
