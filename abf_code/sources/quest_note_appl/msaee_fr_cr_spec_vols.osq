/*-s--------------------------------------------------------------------
Volume Creation
--------------------------------------------------------------------e-*/

INITIALIZE () =

DECLARE h_retries_allowed   = INTEGER1 NOT NULL,
        h_retries           = INTEGER1 NOT NULL,
        h_retry_or_not      = CHAR(5) NOT NULL,
        h_frpr_name         = VARCHAR(24) NOT NULL,
        ret_val             = INTEGER1 NOT NULL,
        latest_period       = INTEGER4 NOT NULL,
        h_display           = INTEGER1 NOT NULL,
        h_question          = INTEGER4 NOT NULL,
        h_unit_1            = CHAR(20) NOT NULL,
        unit_1              = CHAR(20) NOT NULL,
        unit_2              = CHAR(20) NOT NULL,
	h_yesno		    = CHAR(1) NOT NULL,
	h_prefix	    = VARCHAR(15) NOT NULL

{
    h_retries_allowed = 3;
    h_frpr_name = 'msaee_fr_cr_spec_vols';
    msa_date = 'now';
    h_prefix = 'New volume - ';
    /*------------------------------------------------------------------
     Get latest quarterly period and convert it to annual -
     don't allow creation for periods before
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT MAX(period) AS latest_period
        FROM   inquiry_period
        WHERE  inquiry = 14;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing inquiry_period'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows found on inquiry_period'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    latest_period = :latest_period / 100 * 100;
    question2 = 'N';
    question4 = 'N';
    question5 = 'N';
    question6 = 'N';
    question8 = 'N';

    SET_FORMS FIELD '' (REVERSE(period)= 0,
                        DISPLAYONLY(period)= 0,
                        UNDERLINE(period)= 1,
                        REVERSE(inquiry)= 0,
                        DISPLAYONLY(inquiry)= 0,
                        UNDERLINE(inquiry)= 1,
                        REVERSE(question_industry)= 0,
                        DISPLAYONLY(question_industry)= 0,
                        UNDERLINE(question_industry)= 1,
                        REVERSE(question)= 0,
                        DISPLAYONLY(question)= 0,
                        UNDERLINE(question)= 1,
                        REVERSE(question2)= 0,
                        DISPLAYONLY(question2)= 0,
                        UNDERLINE(question2)= 1,
                        REVERSE(question4)= 0,
                        DISPLAYONLY(question4)= 0,
                        UNDERLINE(question4)= 1,
                        REVERSE(question5)= 0,
                        DISPLAYONLY(question5)= 0,
                        UNDERLINE(question5)= 1,
                        REVERSE(question6)= 0,
                        DISPLAYONLY(question6)= 0,
                        UNDERLINE(question6)= 1,
                        REVERSE(question8)= 0,
                        DISPLAYONLY(question8)= 0,
                        UNDERLINE(question8)= 1);
}

FIELD period =
{
    IF :period <> :latest_period AND :period <> :latest_period + 100
    THEN
        MESSAGE 'The only periods that Questions may be created for' +
                ' are ' + VARCHAR(:latest_period) + ' and ' +
                VARCHAR(:latest_period + 100)
                WITH STYLE = POPUP;
                RESUME FIELD period;
    ENDIF;

    SET_FORMS FIELD '' (REVERSE(period)= 1,
                        DISPLAYONLY(period)= 1,
                        UNDERLINE(period)= 0);

    RESUME FIELD inquiry;
}

FIELD inquiry =
{
    IF inquiry <> 14 AND inquiry <> 15
    THEN
        MESSAGE 'Inquiry must be 14 or 15'
        WITH STYLE = POPUP;
        RESUME FIELD inquiry;
    ENDIF;

    SET_FORMS FIELD '' (REVERSE(inquiry)= 1,
                        DISPLAYONLY(inquiry)= 1,
                        UNDERLINE(inquiry)= 0);

    RESUME FIELD question_industry;

}

FIELD question_industry =
{
    MESSAGE 'Checking Industry . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT industry AS question_industry
        FROM   industry
        WHERE  industry = :question_industry
        AND    inquiry = :inquiry;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing industry'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This Industry does not exist for this Inquiry'
        WITH STYLE = POPUP;
        RESUME FIELD question_industry;
    ENDIF;

    SET_FORMS FIELD '' (REVERSE(question_industry)= 1,
                        DISPLAYONLY(question_industry)= 1,
                        UNDERLINE(question_industry)= 0);

    RESUME FIELD question;

}

FIELD question =
{
    IF ASCII(:question) NOT LIKE '%1'
    THEN
        MESSAGE 'Question is not a value 01 code'
        WITH STYLE = POPUP;
        RESUME FIELD question;
    ENDIF;

    MESSAGE 'Checking Question . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question
        FROM   question_library
        WHERE  question = :question
        AND    quest_order = 1
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This Question does not exist for this Period'
        WITH STYLE = POPUP;
        RESUME FIELD question;
    ENDIF;

    SET_FORMS FIELD '' (REVERSE(question)= 1,
                        DISPLAYONLY(question)= 1,
                        UNDERLINE(question)= 0);

    /*-s----------------------------------------------------------------
    Mark the Volumes
    ----------------------------------------------------------------e-*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question AS h_question
        FROM   question_library
        WHERE  period = :period
        AND    question IN (:question + 1, :question + 3, :question + 4,
                            :question + 5, :question + 7)
        {
            IF MOD(:h_question,10) = 2
            THEN
                question2 = 'E';
                SET_FORMS FIELD '' (REVERSE(question2)= 1,
                                    DISPLAYONLY(question2)= 1,
                                    UNDERLINE(question2)= 0);
            ELSEIF MOD(:h_question,10) = 4
            THEN
                question4 = 'E';
                SET_FORMS FIELD '' (REVERSE(question4)= 1,
                                    DISPLAYONLY(question4)= 1,
                                    UNDERLINE(question4)= 0);
            ELSEIF MOD(:h_question,10) = 5
            THEN
                question5 = 'E';
                SET_FORMS FIELD '' (REVERSE(question5)= 1,
                                    DISPLAYONLY(question5)= 1,
                                    UNDERLINE(question5)= 0);
            ELSEIF MOD(:h_question,10) = 6
            THEN
                question6 = 'E';
                SET_FORMS FIELD '' (REVERSE(question6)= 1,
                                    DISPLAYONLY(question6)= 1,
                                    UNDERLINE(question6)= 0);
            ELSEIF MOD(:h_question,10) = 8
            THEN
                question8 = 'E';
                SET_FORMS FIELD '' (REVERSE(question8)= 1,
                                    DISPLAYONLY(question8)= 1,
                                    UNDERLINE(question8)= 0);
            ENDIF;
        };

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7 THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'Clear' =
{
    CLEAR FIELD ALL;
    msa_date = 'now';
    question2 = 'N';
    question4 = 'N';
    question5 = 'N';
    question6 = 'N';
    question8 = 'N';

    SET_FORMS FIELD '' (REVERSE(period)= 0,
                        DISPLAYONLY(period)= 0,
                        UNDERLINE(period)= 1,
                        REVERSE(inquiry)= 0,
                        DISPLAYONLY(inquiry)= 0,
                        UNDERLINE(inquiry)= 1,
                        REVERSE(question_industry)= 0,
                        DISPLAYONLY(question_industry)= 0,
                        UNDERLINE(question_industry)= 1,
                        REVERSE(question)= 0,
                        DISPLAYONLY(question)= 0,
                        UNDERLINE(question)= 1,
                        REVERSE(question2)= 0,
                        DISPLAYONLY(question2)= 0,
                        UNDERLINE(question2)= 1,
                        REVERSE(question4)= 0,
                        DISPLAYONLY(question4)= 0,
                        UNDERLINE(question4)= 1,
                        REVERSE(question5)= 0,
                        DISPLAYONLY(question5)= 0,
                        UNDERLINE(question5)= 1,
                        REVERSE(question6)= 0,
                        DISPLAYONLY(question6)= 0,
                        UNDERLINE(question6)= 1,
                        REVERSE(question8)= 0,
                        DISPLAYONLY(question8)= 0,
                        UNDERLINE(question8)= 1);
    RESUME FIELD period;
}

'Go', KEY FRSKEY4 =
{
    /*-s----------------------------------------------------------------
     Check to see if data entry fields are display_only or not. If they
     are not, they have not been filled in
    ----------------------------------------------------------------e-*/

    INQUIRE_FORMS FIELD '' (h_display = DISPLAYONLY(period));
    IF :h_display = 0
    THEN
        MESSAGE 'Please complete the Period' WITH STYLE = POPUP;
        RESUME FIELD period;
    ENDIF;

    INQUIRE_FORMS FIELD '' (h_display = DISPLAYONLY(inquiry));
    IF :h_display = 0
    THEN
        MESSAGE 'Please complete the Inquiry' WITH STYLE = POPUP;
        RESUME FIELD inquiry;
    ENDIF;

    INQUIRE_FORMS FIELD '' (h_display = DISPLAYONLY(question_industry));
    IF :h_display = 0
    THEN
        MESSAGE 'Please complete the Industry' WITH STYLE = POPUP;
        RESUME FIELD question_industry;
    ENDIF;

    INQUIRE_FORMS FIELD '' (h_display = DISPLAYONLY(question));
    IF :h_display = 0
    THEN
        MESSAGE 'Please complete the Question' WITH STYLE = POPUP;
        RESUME FIELD question;
    ENDIF;

    /*------------------------------------------------------------------
     Check Volumes
    ------------------------------------------------------------------*/

    IF :question2 <> 'Y' AND :question2 <> 'N' AND :question2 <> 'E'
    THEN
        MESSAGE 'Please type Y or N' WITH STYLE = POPUP;
        RESUME FIELD question2;
    ENDIF;

    IF :question4 <> 'Y' AND :question4 <> 'N' AND :question4 <> 'E'
    THEN
        MESSAGE 'Please type Y or N' WITH STYLE = POPUP;
        RESUME FIELD question4;
    ENDIF;

    IF :question5 <> 'Y' AND :question5 <> 'N' AND :question5 <> 'E'
    THEN
        MESSAGE 'Please type Y or N' WITH STYLE = POPUP;
        RESUME FIELD question5;
    ENDIF;

    IF :question6 <> 'Y' AND :question6 <> 'N' AND :question6 <> 'E'
    THEN
        MESSAGE 'Please type Y or N' WITH STYLE = POPUP;
        RESUME FIELD question6;
    ENDIF;

    IF :question8 <> 'Y' AND :question8 <> 'N' AND :question8 <> 'E'
    THEN
        MESSAGE 'Please type Y or N' WITH STYLE = POPUP;
        RESUME FIELD question8;
    ENDIF;

    IF question2 <> 'Y' AND question4 <> 'Y' AND question5 <> 'Y'
    AND question6 <> 'Y' AND question8 <> 'Y'
    THEN
        MESSAGE 'Please mark Volumes to be created with a ''Y'''
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    IF question4 = 'Y' AND question2 = 'N'
    THEN
        MESSAGE 'You are not allowed to create a 2nd volume without' +
                ' creating a 1st volume'
        WITH STYLE = POPUP;
        question4 = 'N';
        RESUME FIELD question2;
    ENDIF;

    IF question5 = 'Y' AND question2 = 'N'
    THEN
        MESSAGE 'You cannot have an 05 volume code without its' +
                ' corresponding 02 code'
        WITH STYLE = POPUP;
        RESUME FIELD question2;
    ENDIF;

    IF question6 = 'Y' AND question4 = 'N'
    THEN
        MESSAGE 'You cannot have a 06 volume code without its' +
                ' corresponding 04 code'
        WITH STYLE = POPUP;
        RESUME FIELD question4;
    ENDIF;

    IF question6 = 'Y' AND question5 = 'N'
    THEN
        MESSAGE 'You cannot have a 06 volume code without its' +
                ' corresponding 05 code'
        WITH STYLE = POPUP;
        RESUME FIELD question5;
    ENDIF;

    IF question8 = 'Y' AND question2 = 'N'
    THEN
        MESSAGE 'You cannot have a 08 volume code without its' +
                ' corresponding 02 code'
        WITH STYLE = POPUP;
        RESUME FIELD question2;
    ENDIF;

    IF question6 = 'N' AND question5 = 'Y' AND question4 = 'Y'
    THEN
        MESSAGE 'If you have both the 04 and the 05 Questions, you' +
                ' must have the corresponding 06 Question'
        WITH STYLE = POPUP;
        RESUME FIELD question6;
    ENDIF;

    /*-s----------------------------------------------------------------
     Create 02 Volume
    ----------------------------------------------------------------e-*/

    IF question2 = 'Y'
    THEN
        unit_1 = '';
        ret_val = CALLFRAME msaee_fr_cr_quest_vols (
                  inquiry             = :inquiry,
                  question_industry   = :question_industry,
                  h_quest_order       = 1,
                  uk_only             = 'N',
                  unit_type_is_preset = 'N',
                  unit_1              = BYREF(:unit_1),
                  question            = :question + 1,
                  period              = :period);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*-s----------------------------------------------------------------
     Create 04 Volume
    ----------------------------------------------------------------e-*/

    IF question4 = 'Y'
    THEN
        unit_2 = '';
        ret_val = CALLFRAME msaee_fr_cr_quest_vols (
                  inquiry             = :inquiry,
                  question_industry   = :question_industry,
                  h_quest_order       = 1,
                  uk_only             = 'N',
                  unit_type_is_preset = 'N',
                  unit_1              = BYREF(:unit_2),
                  question            = :question + 3,
                  period              = :period);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*-s----------------------------------------------------------------
     In case the 02 and 04 Questions were created prior to calling
     this program, get the unit_1 and unit_2 volume types before
     creating the subsequent volumes
    ----------------------------------------------------------------e-*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question AS h_question,
               unit_1 AS h_unit_1
        FROM   question_library
        WHERE  period = :period
        AND    question IN (:question + 1, :question + 3)
        {
            IF MOD(:h_question,10) = 2
            THEN
                unit_1 = :h_unit_1;
            ELSE
                unit_2 = :h_unit_1;
            ENDIF;
        };

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No 02/04 Questions found on question_library'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*-s----------------------------------------------------------------
     Create 05 Volume
    ----------------------------------------------------------------e-*/

    IF question5 = 'Y'
    THEN
        ret_val = CALLFRAME msaee_fr_cr_quest_vols (
                  inquiry             = :inquiry,
                  question_industry   = :question_industry,
                  h_quest_order       = 1,
                  unit_type_is_preset = 'Y',
                  uk_only             = 'N',
                  unit_1              = :unit_1,
                  question            = :question + 4,
                  period              = :period);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*-s----------------------------------------------------------------
     Create 06 Volume
    ----------------------------------------------------------------e-*/

    IF question6 = 'Y'
    THEN
        ret_val = CALLFRAME msaee_fr_cr_quest_vols (
                  inquiry             = :inquiry,
                  question_industry   = :question_industry,
                  h_quest_order       = 1,
                  uk_only             = 'N',
                  unit_type_is_preset = 'Y',
                  unit_1              = :unit_2,
                  question            = :question + 5,
                  period              = :period);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*-s----------------------------------------------------------------
     Create 08 Volume
    ----------------------------------------------------------------e-*/

    IF question8 = 'Y'
    THEN
        ret_val = CALLFRAME msaee_fr_cr_quest_vols (
                  inquiry             = :inquiry,
                  question_industry   = :question_industry,
                  h_quest_order       = 1,
                  unit_type_is_preset = 'Y',
                  uk_only             = 'N',
                  unit_1              = :unit_1,
                  question            = :question + 7,
                  period              = :period);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;


    /*----------------------------------------------------------
    get reason for change and update question_changes table
    -----------------------------------------------------------*/

    h_yesno = '';

    ret_val = CALLPROC msaee_fp_insert_q_changes (
	      question		= :question,
	      period		= :period,
	      h_yesno		= :h_yesno,		
	      h_prefix		= :h_prefix);

    IF ret_val <> 0
    THEN
	RETURN ret_val;
    ENDIF;

    IF ret_val = 9
    THEN 
        RETURN 9;
    ENDIF;


    MESSAGE 'Volume creation completed OK' WITH STYLE = POPUP;

    CLEAR FIELD ALL;
    msa_date = 'now';

    question2 = 'N';
    question4 = 'N';
    question5 = 'N';
    question6 = 'N';
    question8 = 'N';

    SET_FORMS FIELD '' (REVERSE(period)= 0,
                        DISPLAYONLY(period)= 0,
                        UNDERLINE(period)= 1,
                        REVERSE(inquiry)= 0,
                        DISPLAYONLY(inquiry)= 0,
                        UNDERLINE(inquiry)= 1,
                        REVERSE(question_industry)= 0,
                        DISPLAYONLY(question_industry)= 0,
                        UNDERLINE(question_industry)= 1,
                        REVERSE(question)= 0,
                        DISPLAYONLY(question)= 0,
                        UNDERLINE(question)= 1,
                        REVERSE(question2)= 0,
                        DISPLAYONLY(question2)= 0,
                        UNDERLINE(question2)= 1,
                        REVERSE(question4)= 0,
                        DISPLAYONLY(question4)= 0,
                        UNDERLINE(question4)= 1,
                        REVERSE(question5)= 0,
                        DISPLAYONLY(question5)= 0,
                        UNDERLINE(question5)= 1,
                        REVERSE(question6)= 0,
                        DISPLAYONLY(question6)= 0,
                        UNDERLINE(question6)= 1,
                        REVERSE(question8)= 0,
                        DISPLAYONLY(question8)= 0,
                        UNDERLINE(question8)= 1);
    RESUME FIELD period;

}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
