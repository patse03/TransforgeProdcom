/*-s--------------------------------------------------------------------
 Views text of a note
--------------------------------------------------------------------e-*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        h_text            = VARCHAR(62) NOT NULL,
        h_line_no         = INTEGER1 NOT NULL,
        ret_val           = INTEGER1 NOT NULL
{
    h_retries_allowed = 3;
    msa_date = 'now';
    h_frpr_name = 'msaee_fr_vi_note_text';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT   note_text AS h_text,
                 line_no AS h_line_no
        FROM     note_text_line
        WHERE    note_code = :note_code
        AND      period    = :period
        ORDER BY h_line_no 
        {
            IF h_line_no = 1
            THEN
                line1 = :h_text;
            ELSEIF h_line_no = 2
            THEN
                line2 = :h_text;
            ELSEIF h_line_no = 3
            THEN
                line3 = :h_text;
            ELSEIF h_line_no = 4
            THEN
                line4 = :h_text;
            ELSEIF h_line_no = 5
            THEN
                line5 = :h_text;
            ELSEIF h_line_no = 6
            THEN
                line6 = :h_text;
            ELSEIF h_line_no = 7
            THEN
                line7 = :h_text;
            ELSEIF h_line_no = 8
            THEN
                line8 = :h_text;
            ELSEIF h_line_no = 9
            THEN
                line9 = :h_text;
            ELSEIF h_line_no = 10
            THEN
                line10 = :h_text;
            ELSEIF h_line_no = 11
            THEN
                line11 = :h_text;
            ENDIF;
        };

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing note_text_line'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This Note does not exist for this Period'
        WITH STYLE = POPUP;
        RETURN 0;
    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'LinkedQuestions', KEY FRSKEY4 =
{
    ret_val = CALLFRAME msaee_fr_link_quests (
              note_code = :note_code,
              period    = :period);

    msa_date = 'now';
}

'End', KEY FRSKEY6 = 
{
    RETURN 0; 
}
