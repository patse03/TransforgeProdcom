
INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL,
        h_yesno           = CHAR(1) NOT NULL,
        h_counter         = INTEGER4 NOT NULL

{
    msa_date = 'now';
    h_retries_allowed = 3;
    h_frpr_name = 'msaee_fr_de_quest_link';
}

FIELD period =
{
    MESSAGE 'Checking Period . . .';

    ret_val = CALLPROC msaee_fp_check_year (period = :period);
    IF ret_val = 9
    THEN
        RETURN ret_val;
    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This is not a valid annual Period'
        WITH STYLE = POPUP;
        RESUME FIELD period;
    ENDIF;

    RESUME FIELD question;
}

FIELD question =
{
    MESSAGE 'Checking Question . . .';

    /*------------------------------------------------------------------
     Check Question is valid
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT question
        FROM   question_library
        WHERE  question = :question
        AND    MOD(question,10) = 1
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'This is not a valid Value Question for this Period'
        WITH STYLE = POPUP;
        RESUME FIELD question; 
    ENDIF;

    /*------------------------------------------------------------------
     See if link exists
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT COUNT(*) AS h_counter
        FROM   question_note_link
        WHERE  question = :question
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_note_link'
        WITH STYLE = POPUP;
        RETURN 9;

    ENDIF;

    IF :h_counter = 0
    THEN
        MESSAGE 'There are no Notes linked to this Question for' +
                ' this Period'
        WITH STYLE = POPUP;
        RESUME FIELD question;
    ENDIF;

    h_yesno = '';

    WHILE h_yesno <> 'y' AND h_yesno <> 'n'
    DO
        h_yesno = PROMPT VARCHAR(:h_counter) + ' Notes are' +
                         ' linked to Question ' + VARCHAR(:question) + 
                         ' for ' + VARCHAR(:period) + '. Do you wish' +
                         ' to delete them? Type Y/N and press <return>'
                  WITH STYLE = POPUP; 
        h_yesno = LOWERCASE(h_yesno);
    ENDWHILE;

    IF h_yesno = 'y'
    THEN

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;
    
        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO
    
            h_retries = :h_retries + 1;
    
            DELETE FROM question_note_link
            WHERE       question = :question
            AND         period = :period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);
    
            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;
    
        ENDWHILE;
    
        IF ret_val = 7
        THEN
            RETURN 7;
    
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in accessing question_note_link'
            WITH STYLE = POPUP;
            RETURN 9;
    
        ELSEIF ret_val = 1
        THEN
            MESSAGE 'Unable to delete from question_note_link'
            WITH STYLE = POPUP;
            RETURN 9;
        ENDIF;
    
        MESSAGE 'Links deleted OK' WITH STYLE = POPUP;
    ELSE
        MESSAGE 'No Links deleted, as requested' WITH STYLE = POPUP;
    ENDIF;

    CLEAR FIELD ALL;
    msa_date = 'now';
    RESUME FIELD period;
}    

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'Clear' =
{
    CLEAR FIELD ALL;
    msa_date = 'now';
    RESUME FIELD period;
}

'End', KEY FRSKEY6 = 
{
    RETURN 0; 
}
