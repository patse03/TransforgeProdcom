/*---------------------------------------------------------------------e

  The big takeon frame - load_codes
----------------------------------------------------------------------*/

INITIALIZE (

        h_dummy                      = CHAR(11) NOT NULL,
        h_idbr                       = CHAR(11) NOT NULL,
        form_taken_on                = CHAR(1) NOT NULL,
        prev_period                  = INTEGER4 NOT NULL,
        correct_date                 = DATE NOT NULL,
        alt_industry                 = INTEGER4 NOT NULL,
        currency                     = CHAR(1) NOT NULL,
        temp_del_aos                 = VARCHAR(20) NOT NULL,
        temp_cqpv                    = VARCHAR(20) NOT NULL) =

DECLARE h_frpr_name                  = CHAR(24) NOT NULL,
        h_rowstate                   = INTEGER4 NOT NULL,
        h_counter                    = INTEGER4 NOT NULL,
        h_counter1                   = INTEGER4 NOT NULL,
        h_data_vetted                = CHAR(1) NOT NULL,
        h_dummy_present              = CHAR(1) NOT NULL,
        h_imp_present                = CHAR(1) NOT NULL,
        h_mancon                     = CHAR(1) NOT NULL,
        h_euro_question              = INTEGER4 NOT NULL,
        h_sterling                   = INTEGER4 NOT NULL,
        h_look_for_question          = INTEGER4 NOT NULL,
        h_employment                 = INTEGER4 NOT NULL,
        h_value_of_01                = INTEGER4 NOT NULL,
        vol_question2                = INTEGER4 NOT NULL,
        vol_question4                = INTEGER4 NOT NULL,
        acell_question2              = CHAR(2) NOT NULL,
        acell_question4              = CHAR(2) NOT NULL,
        h_command                    = INTEGER1 NOT NULL,
        tf.quest_order               = INTEGER1 NOT NULL,
        tf.h_col                     = INTEGER4 NOT NULL,
        tf.question_industry         = INTEGER4 NOT NULL,
        tf.question_inquiry          = INTEGER1 NOT NULL,
        tf.aconstruction_type        = INTEGER1 NOT NULL,
        tf.update_type               = CHAR(1) NOT NULL,
        tf.acell_type                = CHAR(2) NOT NULL,
        h_aconstruction_type         = INTEGER1 NOT NULL,
        h_acell_type                 = CHAR(2)  NOT NULL,
        store_01                     = INTEGER4 NOT NULL,
        store_val                    = INTEGER4 NOT NULL,
        ret_val                      = INTEGER4 NOT NULL,
        h_insert_count               = INTEGER4 NOT NULL,
        h_record                     = INTEGER4 NOT NULL,
        h_figures_changed            = CHAR(1) NOT NULL,
        h_questions_changed          = CHAR(1) NOT NULL,
        store_vol                    = INTEGER4 NOT NULL,
        h_yesno                      = CHAR(1) NOT NULL,
        h_yesno2                     = CHAR(1) NOT NULL,
        h_new_question               = INTEGER4 NOT NULL,
        h_annual_period              = INTEGER4 NOT NULL,
        euro_t_sales                 = INTEGER4 NOT NULL,
        euro_t_of_01                 = INTEGER4 NOT NULL,
        h_retries_allowed            = INTEGER1 NOT NULL,
        h_retries                    = INTEGER1 NOT NULL,
        h_retry_or_not               = CHAR(5) NOT NULL,
        h_mismatch_question          = INTEGER4 NOT NULL,
        h_question                   = INTEGER4 NOT NULL,
        h_message                    = VARCHAR(200) NOT NULL,
        h_temp_table                 = VARCHAR(24) NOT NULL,
        h_add_name1                  = VARCHAR(35) NOT NULL,
        h_new_total_sales            = INTEGER4 NOT NULL,
        h_save_total_sales           = INTEGER4 NOT NULL,
        h_atypical                   = CHAR(1) NOT NULL,
        h_confirm                    = CHAR(1) NOT NULL,
        h_rows                       = INTEGER2 NOT NULL,
        h_temp_del_table             = VARCHAR(20) NOT NULL,
        h_industry_count             = INTEGER1 NOT NULL,
        prev_period1                 = INTEGER4 NOT NULL,
        lp_update_temp_cqpv          = PROCEDURE RETURNING INTEGER,
        lp_load_tf                   = PROCEDURE RETURNING INTEGER,
        lp_check_credibility         = PROCEDURE RETURNING INTEGER
{
    msa_date = 'now';
    h_frpr_name = 'msab_fr_load_codes';
    h_retries_allowed = 3;
    euro_msg = 'EURO CONTRIBUTOR';
    prev_qtrly = '';

    h_counter = 0;
    h_counter1 = 1;
    h_questions_changed = 'n';
    tf.h_col = 0;
    h_command = 0;
    h_confirm = 'n';

    IF country = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(country) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(country) = 0);
    ENDIF;

    IF key_disp = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(key_disp) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(key_disp) = 0);
    ENDIF;

    IF data_cont = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(cont_link) = 1);
        SET_FORMS FIELD '' (INVISIBLE(data_cont) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(data_cont) = 0);
        SET_FORMS FIELD '' (INVISIBLE(cont_link) = 0);
    ENDIF;

    IF clink = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(clink) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(clink) = 0);
    ENDIF;

    IF qlink = ''
    THEN
        SET_FORMS FIELD '' (INVISIBLE(qlink) = 1);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(qlink) = 0);
    ENDIF;

    IF currency = 'E'
    THEN
        SET_FORMS FIELD '' (INVISIBLE(euro_msg) = 0,
                            INVISIBLE(exchange_rate) = 0);
        SET_FORMS COLUMN '' tf (INVISIBLE(evalue) = 0,
                                DISPLAYONLY(avalue) = 1);

    ELSE
        SET_FORMS FIELD '' (INVISIBLE(euro_msg) = 1,
                            INVISIBLE(exchange_rate) = 1);
        SET_FORMS COLUMN '' tf (INVISIBLE(evalue) = 1,
                                DISPLAYONLY(avalue) = 0);
    ENDIF;

    SET_FORMS FIELD '' (INVISIBLE(total_of_01)=1,
                        INVISIBLE(total_sales)=1,
                        INVISIBLE(difference)=1);

    INITTABLE tf UPDATE;

    MESSAGE 'Selecting Contributor Values . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        INSERT INTO :temp_cqpv (
                    quest_order,
                    question,
                    acell,
                    avalue,
                    evalue,
                    acell_type,
		    rounded,
                    aconstruction_type,
                    confirmation_code,
                    question_industry,
                    question_inquiry,
                    update_type,
                    error_msg,
		    atypical)
        SELECT      quest_order,
                    question,
                    acell,
                    avalue,
                    evalue,
                    acell_type,
		    ' ',
                    aconstruction_type,
                    confirmation_code,
                    question_industry,
                    contributor_inquiry,
                    'u',
                    '',
                    ''
        FROM        cqpv
        WHERE       contributor_inquiry   = :inquiry
        AND         contributor_reference = :contributor_reference
        AND         period                = :period
        AND         contributor_industry  = :contributor_industry;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error accessing cqpv table'
        WITH STYLE = POPUP;
        RETURN 9;
    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Contributor has no questions for this period'
        WITH STYLE = POPUP;
        RETURN 1;
    ELSEIF ret_val = 0
    THEN
            tf= SELECT question 
                FROM   :temp_cqpv a
                WHERE  question IN(
                       SELECT question
                       FROM   question_library b
                       WHERE  period = :period
                       AND    question_industry IN(
                              SELECT industry
                              FROM industry_period
                              WHERE period = 200412 ));

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck = 1,
                      h_rollback   = 'N',
                      h_commit     = 'Y',
                      h_retries    = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name  = :h_frpr_name);

            IF ret_val = 9
            THEN
                MESSAGE 'System error re-loading questions'
                WITH STYLE = POPUP;
                RETURN 9;
            ELSEIF ret_val = 0
            THEN
                IF :period <= 200400
                then
                    prev_qtrly = 'WARNING:Form holds questions that ' +
		                'were quarterly, only amend annual questions. ';
                else
                    prev_qtrly = 'WARNING:Form holds questions that ' +
                                 'were quarterly. ';
                endif;

                SET_FORMS FIELD '' (INVISIBLE(prev_qtrly) = 0);

            ENDIF; 

        IF prev_qtrly = ''
        THEN
            SET_FORMS FIELD '' (INVISIBLE(prev_qtrly) = 1);
        ELSE
            SET_FORMS FIELD '' (INVISIBLE(prev_qtrly) = 0);
        ENDIF;
        /*RESUME FIELD period;*/
    ENDIF;

    /*------------------------------------------------------------------
     Loads up R code for questions rounded as probable exact £ returns.
    ------------------------------------------------------------------*/

    UPDATE :temp_cqpv a
      FROM avalue_autoedit b
       SET rounded = 'R'
     WHERE RIGHT(:temp_cqpv,11) = b.contributor
       AND a.question = b.question
       AND b.period = :period
       AND LEFT(b.action_taken,1) = 'R';

    ret_val = CALLPROC check_inq_ing (
                       h_num_row_ck = 1,
                       h_rollback   = 'N',
                       h_commit     = 'Y',
                       h_retries    = :h_retries,
                       h_retries_allowed = :h_retries_allowed,
                       h_frpr_name  = :h_frpr_name);

    IF ret_val > 1
    THEN
        MESSAGE 'SYSTEM ERROR: Unable to load rounded markers. Error code '
	      + VARCHAR(:ret_val) +'. Please contact IM.'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Modify
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        MODIFY      :temp_cqpv TO BTREE
        UNIQUE ON   quest_order,
                    question;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in modifying ' + :temp_cqpv
        WITH STYLE = POPUP;
        RETURN 9;

    ENDIF;

    /*------------------------------------------------------------------
     Replace evalue volumes by the avalues
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE :temp_cqpv
        SET    evalue = avalue
        WHERE  MOD(question,10) <> 1 AND MOD(question,10000) <> 9200;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error updating evalues on ' + :temp_cqpv
        WITH STYLE = POPUP;
        RETURN 9;

    /* COMMENTED OUT AS THIS WAS CAUSING AN ERROR WHEN 9100 REMOVED
       OCTOBER 2001
    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows updated for evalues on ' + :temp_cqpv
        WITH STYLE = POPUP;
        RETURN 9;
*/
    ENDIF;

    /*------------------------------------------------------------------
     Find annual period - for the periodic question_library
    ------------------------------------------------------------------*/

    h_annual_period = :period / 100 * 100;

    /*------------------------------------------------------------------
     Update with question's inquiry from the periodic question_library
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        UPDATE  :temp_cqpv a
        FROM    question_library b
        SET     question_inquiry = b.inquiry
        WHERE   a.question = b.question
        AND     a.quest_order = b.quest_order
        AND     b.period = :h_annual_period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error accessing question_library'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    /*------------------------------------------------------------------
    If the form is imputed or has been constructed for a non-responder,
    prompt before getting rid of the figures.
    ------------------------------------------------------------------*/

    h_imp_present = 'n';
    h_mancon = 'n';

    UNLOADTABLE tf
    {
        IF tf.acell = 'I'
        THEN
            h_imp_present = 'y';

        ELSEIF tf.aconstruction_type = 6
        THEN
            h_mancon = 'y';
        ENDIF;
    };

    IF h_imp_present = 'y' OR h_mancon = 'y'
    THEN
        IF h_imp_present = 'y'
        THEN
            h_message = 'This form is an imputed form.';
        ELSE
            h_message = 'This form has been manually constructed.';
        ENDIF;

        h_yesno = '';
        WHILE h_yesno <> 'n' AND h_yesno <> 'y'
        DO
            h_yesno =  PROMPT :h_message +
                              ' Continuing will remove all values.' +
                              ' Do you wish to continue?' +
                              ' Type Y/N and press <return>'
            WITH STYLE = POPUP;
            h_yesno = LOWERCASE(h_yesno);
        ENDWHILE;

        IF h_yesno = 'n'
        THEN
            RETURN 0;
        ENDIF;

        UNLOADTABLE tf
        {
            tf.evalue = 0;
            tf.avalue = 0;
            tf.acell = 'N';
            tf.acell_type = '';
            tf.confirmation_code = '';
            tf.aconstruction_type = 0;
            tf.error_msg = '';
        };

        ret_val = CALLPROC lp_update_temp_cqpv ();
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;

    ELSE

        /*--------------------------------------------------------------
        Don't do the initial checking of values for Ns (non-responders)
        --------------------------------------------------------------*/

        UNLOADTABLE tf
        {
            IF tf.acell = 'N'
            THEN
                RESUME;
            ENDIF;
        };

        /*--------------------------------------------------------------
        Do Credibility Checking Initially to load up the error codes
        --------------------------------------------------------------*/

        ret_val = CALLPROC lp_check_credibility ();
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;

        ret_val = CALLPROC lp_load_tf ();
        IF ret_val <> 0
        THEN
           RETURN ret_val;
        ENDIF;

    ENDIF;

    /*------------------------------------------------------------------
    Store the values on cqpv in case the cell codes have changed
    ------------------------------------------------------------------*/

    MESSAGE 'Saving Initial Values . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE cqpv a
        FROM   :temp_cqpv b
        SET    acell                   = b.acell,
               avalue                  = b.avalue,
               acell_type              = b.acell_type,
               aconstruction_type      = b.aconstruction_type,
               confirmation_code       = b.confirmation_code
        WHERE  a.contributor_inquiry   = :inquiry
        AND    a.contributor_reference = :contributor_reference
        AND    a.question              = b.question
        AND    a.quest_order           = b.quest_order
        AND    a.period                = :period
        AND    a.contributor_industry  = :contributor_industry
        AND    b.update_type           = 'u';

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in initial updating of cqpv'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows updated initially on cqpv'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    h_figures_changed = 'n';

    /*------------------------------------------------------------------
    Wipe out correct_date if there are errors on it
    ------------------------------------------------------------------*/

    IF h_data_vetted = 'n' AND correct_date <> ''
    THEN

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            UPDATE contributor_period
            SET    correct_date = '',
                   corr_week_no = 0,
                   form_status = 2
            WHERE  contributor_reference = :contributor_reference
            AND    period = :period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;

        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in initial updating of contributor_period'
            WITH STYLE = POPUP;
            RETURN 9;

        ELSEIF ret_val = 1
        THEN
            MESSAGE 'Error in initial resetting of contributor_period'
            WITH STYLE = POPUP;
            RETURN 9;

        ENDIF;
    ENDIF;

}

/*----------------------------------------------------------------------
  Checks which key has been pressed to move out of a row and takes the
  necessary action test out of data
----------------------------------------------------------------------*/

FIELD tf.avalue =
{
    INQUIRE_FORMS FRS (h_command = command);

    IF (:h_command = 6   /*  down arrow  */
    OR :h_command = 4    /*  tab         */
    OR :h_command = 8)   /*  return      */
    THEN
        IF :tf.h_col = :h_counter
        THEN
            MESSAGE 'End of Form';
            /*SLEEP 1;*/
        ELSE
            h_counter1 = :tf.h_col + 1;
            SCROLL tf TO :h_counter1;
        ENDIF;

    ELSEIF h_command = 7  /*   up arrow    */
    THEN
        IF :tf.h_col = 1
        THEN
            MESSAGE 'This is the first question';
            /*SLEEP 1;*/
        ELSE
            h_counter1 = :tf.h_col - 1;
            SCROLL tf TO :h_counter1;
        ENDIF;

    ELSEIF :h_command = 10  /*   pagedown  */
    THEN
        IF :tf.h_col = :h_counter
        THEN
            MESSAGE 'End of Form';
            /*SLEEP 1;*/
        ELSEIF :tf.h_col > (h_counter - 10)
        THEN
            SCROLL tf TO :h_counter;
        ELSE
            h_counter1 = :tf.h_col + 10;
            SCROLL tf TO :h_counter1;
        ENDIF;

    ELSEIF :h_command = 11
    THEN
        IF :tf.h_col = 1
        THEN
            MESSAGE 'This is the first question';
            /*SLEEP 1;*/
        ELSEIF :tf.h_col < 11
        THEN
            SCROLL tf TO 1;
        ELSE
            h_counter1 = :tf.h_col - 10;
            SCROLL tf TO :h_counter1;
        ENDIF;
    ELSE
        h_counter1 = :tf.h_col + 1;
        SCROLL tf TO :h_counter1;
    ENDIF;
}

'Confirm' =
{
    msa_date = 'now';
    IF tf.acell  = 'N'
    THEN
        MESSAGE 'You cannot confirm a figure where the cell code is N'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    IF tf.acell  = 'W'
    THEN
        MESSAGE 'You cannot confirm a figure where the cell code is W'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    /*------------------------------------------------------------------
      If any row has been changed since the form last went through
      form_check, it's got to be checked again.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf (h_rowstate = _state)
    {
        IF h_rowstate <> 2
        THEN
            MESSAGE 'This option must be utilized immediately after ' +
                    'CHECK FORM, before any values have been changed. Please ' +
                    'go through CHECK FORM again, and then use this option ' +
                    'to confirm any values found to be incredible'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;
    };

    h_yesno = '';
    WHILE h_yesno <> 'Q' AND h_yesno <> 'R'
    AND h_yesno <> 'X'
    AND h_yesno <> 'B'
    DO
        h_yesno = PROMPT 'Please give the Type of Confirmation:' +
                         X'0D0D' +
                         'B = Construct in Batch' +
                         ' (No Volume Data Available)' + X'0D' +
                         'Q = Confirm the Current Question' + X'0D' +
                         'R = Remove the Current Question''s' +
                         ' Confirmation' + X'0D' +
                         'X = Quit'
                WITH STYLE = POPUP;
        h_yesno = UPPERCASE(h_yesno);
    ENDWHILE;

    IF h_yesno = 'X'
    THEN
        RESUME;
    ENDIF;

    IF h_yesno = 'B'
    THEN
        IF MOD(tf.question,10) <> 2 AND MOD(tf.question,10) <> 4
        AND MOD(tf.question,10) <> 5 AND MOD(tf.question,10) <> 6
        AND MOD(tf.question,10) <> 8
        THEN
            MESSAGE 'Batch Construction is only be carried out' +
                    ' on Volumes'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        IF tf.avalue <> 0
        THEN
            MESSAGE 'Batch Construction can only be carried out when' +
                    ' the Volume is Zero'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        h_counter1 = tf.h_col;
        IF MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4
        THEN
            h_look_for_question = tf.question / 10 * 10 + 1;
        ELSEIF MOD(tf.question,10) = 5 OR MOD(tf.question,10) = 8
        THEN
            h_look_for_question = tf.question / 10 * 10 + 2;
        ELSE
            h_look_for_question = tf.question / 10 * 10 + 4;
        ENDIF;

        UNLOADTABLE tf
        {
            IF tf.question = h_look_for_question
            THEN
                IF MOD(tf.question,10) = 1 AND tf.avalue = 0
                THEN
                    MESSAGE 'You cannot carry out Batch Construction' +
                            ' on a Volume while its Value Question ' +
                            VARCHAR(tf.question) + ' is zero'
                    WITH STYLE = POPUP;
                    SCROLL tf TO :h_counter1;
                    RESUME;

                ELSEIF MOD(tf.question,10) = 2 AND (tf.avalue <> 0 OR tf.acell <> 'B')
                THEN
                    MESSAGE 'You cannot carry out Batch Construction' +
                            ' on an 05 or 08 Volume until the 02 ' +
                            ' Question ' + VARCHAR(tf.question) +
                            ' is zero and has a B cell code'
                    WITH STYLE = POPUP;
                    SCROLL tf TO :h_counter1;
                    RESUME;

                ELSEIF MOD(tf.question,10) = 4 AND (tf.avalue <> 0 OR tf.acell <> 'B')
                THEN
                    MESSAGE 'You cannot carry out Batch Construction' +
                            ' on an 06 Volume until the 04 ' +
                            ' Question ' + VARCHAR(tf.question) +
                            ' is zero and has a B cell code'
                    WITH STYLE = POPUP;
                    SCROLL tf TO :h_counter1;
                    RESUME;
                ENDIF;
            ENDIF;
        };

        SCROLL tf TO :h_counter1;

        tf.acell = 'B';
        tf.error_msg = '';
        tf.aconstruction_type = 0;

        /*--------------------------------------------------------------
        If the zero volume is set for 'B' construction, clear its
        value question
        --------------------------------------------------------------*/
/*
        IF MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4
        THEN
            h_mismatch_question = tf.question / 10 * 10 + 1;

            UNLOADTABLE tf
            {
                IF tf.question = h_mismatch_question
                THEN
                    tf.acell = 'V';
                    tf.error_msg = '';
                ENDIF;
            };
        ENDIF;
*/
    ELSEIF h_yesno = 'R'
    THEN
        tf.confirmation_code = '';

        IF tf.acell = 'B'
        THEN
            tf.acell = 'Z';
        ENDIF;

    ELSE
        /*--------------------------------------------------------------
         Add the confirmation
        --------------------------------------------------------------*/

        IF tf.acell = 'B'
        THEN
            MESSAGE 'This Value is to be Constructed in Batch:' +
                    ' There is nothing to Confirm.'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        /*--------------------------------------------------------------
        Don't allow confirmation of mismatches
        Amended December 1999:-
        Allow confirmation of zero volumes for ship building type 
        industries as they are built in stages and sometimes completed 
        over more that 1 period. Industries involved are 35110, 35120 
        and 35300
        --------------------------------------------------------------*/
 
        IF tf.question_industry <> 35110 AND tf.question_industry <> 35120
        AND tf.question_industry <> 35300 
        THEN
            IF (MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4)
            AND tf.acell = 'M'
            THEN
                MESSAGE 'You cannot confirm a zero Volume while its' +
                        ' Value is non-zero'
                WITH STYLE = POPUP;
                RESUME;
            ENDIF;
        ENDIF;

        IF (MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4)
        AND tf.acell = 'L'
        THEN
            MESSAGE 'You cannot confirm a non-zero Volume while its' +
                    ' Value is zero'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        /*--------------------------------------------------------------
        Put the confirmation on
        --------------------------------------------------------------*/

        h_yesno = '';
        WHILE h_yesno <> 'C' AND h_yesno <> 'S' AND h_yesno <> 'G'
        AND   h_yesno <> 'X' AND h_yesno <> 'M' AND h_yesno <> 'V'
        DO
            h_yesno = PROMPT 'Please give the Reason for Confirmation:' +
                             X'0D0D' +
                             'C = Contacted Company' + X'0D' +
                             'G = Generally Acceptable' + X'0D' +
                             'M = Mis-Reported' + X'0D' +
                             'S = Seasonality' + X'0D' +
			     'V = Confirmed Zero Volume' + X'0D' +
                             'X = Quit'
                    WITH STYLE = POPUP;
            h_yesno = UPPERCASE(h_yesno);
        ENDWHILE;

        IF h_yesno = 'X'
        THEN
            RESUME;
        ENDIF;

        IF tf.avalue = 0
        THEN
            /*----------------------------------------------------------
            IF tf.quest_order = 11
            THEN
                MESSAGE 'Returned Employment must not be Zero'
                WITH STYLE = POPUP;
                RESUME;
            ELSE
                tf.acell = 'Z';
            ENDIF;
            ----------------------------------------------------------*/
            tf.acell = 'Z';

        ELSE
            IF tf.acell = 'U'
            THEN
                tf.acell = 'V';

            ELSEIF tf.acell = 'K'
            THEN
                tf.acell = 'P';

            ELSEIF tf.acell = '1U'
            THEN
                tf.acell = '1V';

            ELSEIF tf.acell = '1K'
            THEN
                tf.acell = '1P';
            ENDIF;

        ENDIF;

        tf.confirmation_code = h_yesno;
        tf.error_msg = '';

    ENDIF;

    /*------------------------------------------------------------------
    To resume on required question
    ------------------------------------------------------------------*/

    h_counter1 = tf.h_col;

    ret_val = CALLPROC lp_update_temp_cqpv ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    h_confirm = 'y';
}

'QuestionText' =
{
    msa_date = 'now';
    ret_val = CALLPROC ARRAYCLEAR(g_question_array);

    /*------------------------------------------------------------------
    Copy all the tf questions into the array
    ------------------------------------------------------------------*/

    h_counter = 0;

    UNLOADTABLE tf
    {
        INSERTROW g_question_array [:h_counter] (
                  quest_order = :tf.quest_order,
                  question = :tf.question);
        h_counter = h_counter + 1;
    };

    /*------------------------------------------------------------------
    Get the current question's position and pass it through to the
    text displaying program
    ------------------------------------------------------------------*/

    h_counter = 0;

    UNLOADTABLE g_question_array
    {
        h_counter = :h_counter + 1;
        IF :g_question_array.question = :tf.question
        THEN
            ENDLOOP;
        ENDIF;
    };

    h_question = :tf.question;

    ret_val = CALLFRAME msab_fr_dis_ques_text(
              period    = :period,
              industry  = :contributor_industry,
              h_counter = :h_counter,
              question  = BYREF(:h_question));

    /*------------------------------------------------------------------
    Reload and resume on question whose text was looked at most recently
    ------------------------------------------------------------------*/

    ret_val = CALLPROC lp_update_temp_cqpv ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    UNLOADTABLE tf (h_record = _RECORD)
    {
         IF :tf.question = :h_question
         THEN
             SCROLL tf TO :h_record;
             RESUME FIELD tf;
         ENDIF;
    };

}

'ManConst', KEY FRSKEY12 =
{
    msa_date = 'now';
    h_yesno = '';
    WHILE h_yesno <> 'M' AND h_yesno <> 'R'
    AND   h_yesno <> 'X'
    DO
        h_yesno = PROMPT 'Please give the Type of Construction:' +
                         X'0D0D' +
                         'M = Manually Construct the Current Question' +
                         X'0D' +
                         'R = Remove the Current Question''s' +
                         ' Construction' + X'0D' +
                         'X = Quit'
                WITH STYLE = POPUP;
        h_yesno = UPPERCASE(h_yesno);
    ENDWHILE;

    IF h_yesno = 'X'
    THEN
        RESUME;

    ELSEIF h_yesno = 'M'
    THEN
        IF tf.avalue = 0
        THEN
            MESSAGE 'You cannot Manually Construct a zero value'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        tf.acell = 'P';
        tf.aconstruction_type = 1;
        tf.confirmation_code = '';

    ELSE
        /*--------------------------------------------------------------
         Remove the Manual Construction
        --------------------------------------------------------------*/

        IF tf.acell <> 'P'
        THEN
            MESSAGE 'You can only remove a Manual Construction marker' +
                    ' for cell codes of P'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;

        tf.avalue = 0;
        tf.acell = 'N';
        tf.acell_type = '';
        tf.confirmation_code = '';
        tf.aconstruction_type = 0;
        tf.error_msg = '';
    ENDIF;
}

'PrintForm', KEY FRSKEY13 =
{
    msa_date = 'now';

        h_yesno = '';
        WHILE h_yesno <> 'n' AND h_yesno <> 'y'
        DO
            h_yesno =  PROMPT :h_message +
                              ' Do you wish to print form?' +
                              ' Type Y/N and press <return>'
            WITH STYLE = POPUP;
            h_yesno = LOWERCASE(h_yesno);
        ENDWHILE;

        IF h_yesno = 'n'
        THEN
            RESUME;
        ENDIF;

    /*------------------------------------------------------------------
     Print the Whole Forms (plus question text)
    ------------------------------------------------------------------*/

    MESSAGE 'Creating List of Questions . . .';

    h_temp_table = 'temp_takeon_print';

    ret_val = CALLPROC msa_fp_drop_table (
              h_table = :h_temp_table);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        CREATE TABLE :h_temp_table
        AS SELECT    question,
                     quest_order,
                     avalue,
                     evalue,
                     acell,
                     confirmation_code,
                     error_msg,
                     '                                                                 ' AS text_line1
       FROM          :temp_cqpv;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in creating ' + :h_temp_table
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows found to create ' + :h_temp_table
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Add question text
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE :h_temp_table a
        FROM   question_library b
        SET    text_line1 = b.text_line1
        WHERE  a.question = b.question
        AND    b.period = :period / 100 * 100;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in updating question text on ' + :h_temp_table
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No question text updated on ' + :h_temp_table
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    h_add_name1 = add_name1;

    ret_val = CALLPROC msa_fp_remove_quotes (
              textline = BYREF(:h_add_name1));

    MESSAGE 'Preparing Report . . .';

    ret_val = CALLPROC msa_cp_callsys (
              'report ' +
              :g_database +
              ' ''(period = ' + VARCHAR(:period) +
              ',contributor_reference = "' + :contributor_reference + '"' +
              ',add_name1 = "' + :h_add_name1 + '"' +
              ',h_table = "' + :h_temp_table + '"' +
              ',currency = "' + :currency + '"' +
              ',user_name = "' + :g_user_name + '"' +
              ')'' -i$REPORTS/msab_rw_print_form.rw' +
              ' > report.output 2> report.output');

    IF ret_val <> 0
    THEN
        MESSAGE 'Error producing report'
        WITH STYLE = POPUP;
    ELSE
        /*----------------------------------------------------------
         Remove last line - so a blank end page is not printed
        ----------------------------------------------------------*/

        ret_val = CALLPROC msa_cp_callsys (
                  'lines=`wc -l < msab_rw_print_form.rpt`;' +
                  'lines=`expr ${lines} - 1`;' +
                  'head -${lines} msab_rw_print_form.rpt | ' +
                  'lp -o16l -d' + :g_printer +
                  ' >> report.output 2>> report.output');

        IF ret_val <> 0
        THEN
            MESSAGE 'Error sending report to printer ' +
                    :g_printer
            WITH STYLE = POPUP;
        ELSE
            MESSAGE 'Report sent to printer ' + :g_printer + ' OK'
            WITH STYLE = POPUP;
        ENDIF;
    ENDIF;

    ret_val = CALLPROC msa_fp_drop_table (
              h_table = :h_temp_table);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;
}

'ExtraQuests' =
{
    msa_date = 'now';

    /*------------------------------------------------------------------
     Add a real question or a dummy written in code.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf(h_rowstate = _state)
    {
        IF h_rowstate <> 2
        THEN
            MESSAGE 'This option must be utilized immediately after' +
                    ' CHECK FORM, or before any changes are made.' +
                    ' Please go through CHECK FORM again'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;
    };

    ret_val = CALLPROC lp_update_temp_cqpv ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    ret_val = CALLFRAME msab_fr_ext_menu (
              contributor_inquiry   = inquiry,
              contributor_reference = contributor_reference,
              h_dummy               = :h_dummy,
              h_idbr                = :h_idbr,
              prev_period           = :prev_period,
              period                = period,
              contributor_industry  = contributor_industry,
              currency              = :currency,
              h_questions_changed   = BYREF(h_questions_changed),
              temp_cqpv             = :temp_cqpv,
              temp_del_aos          = temp_del_aos);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;
}

'CN Links' =
{
    msa_date = 'now';

    /*------------------------------------------------------------------
     Shows questions associated with a particular CN code
    ------------------------------------------------------------------*/

    CALL APPLICATION (exec = '$IMAGES/idealist_app',
                     param = VARCHAR(:period));
}

'CheckForm' =
{
    msa_date = 'now';

    MESSAGE 'Checking Form Values . . .';

    h_dummy_present = 'n';

    h_temp_del_table = 'temp_deleted_uncoded';

    ret_val = CALLPROC msa_fp_drop_table (
              h_table = :h_temp_del_table);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    /*------------------------------------------------------------------
    Make sure no negative values are on the form
    ------------------------------------------------------------------*/

    UNLOADTABLE tf (h_record = _record)
    {
        IF :tf.avalue < 0
        THEN
            MESSAGE 'Negative Values are not allowed'
            WITH STYLE = POPUP;
            SCROLL tf TO h_record;
            RESUME;
        ENDIF;
    };

    /*------------------------------------------------------------------
    Don't put a non-responder's constructed form through CheckForm
    until the constructions have been removed

    UNLOADTABLE tf
    {
        IF tf.aconstruction_type = 6
        THEN
            MESSAGE 'This Form has been Constructed for a' +
                    ' Non-Responder. Please remove the Construction' +
                    ' Markers before calling CHECK FORM.'
            WITH STYLE = POPUP;
            RESUME;
        ENDIF;
    };
    ------------------------------------------------------------------*/

    /*------------------------------------------------------------------
      If an imputed form is being overwritten, ALL the imputed figures
      must be overwritten.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf(h_rowstate = _state, h_record = _record)
    {
        IF tf.acell = 'I'
        THEN
            IF h_rowstate = 2 AND :tf.avalue <> 0
            THEN
                MESSAGE 'When overwriting a form containing imputed' +
                        ' figures, all of the form must be overwritten'+
                        '.' + X'0D0D' + 'Question ' +
                        VARCHAR(:tf.question) +
                        ' contains a value that has not been changed.'
                WITH STYLE = POPUP;
                SCROLL tf TO h_record;
                RESUME;
            ENDIF;
        ENDIF;
    };

    /*------------------------------------------------------------------
    For a Euro form:
       1. Copy all volumes (evalue -> avalue)
       2. Convert all values (evalue / exchange rate -> avalue)
    ------------------------------------------------------------------*/

    IF currency = 'E'
    THEN
        UNLOADTABLE tf ( h_rowstate = _state)
        {
            IF h_rowstate <> 2
            THEN
                IF MOD(tf.question,10) = 1 OR MOD(tf.question,10000) = 9200
                THEN
                    tf.avalue = (:tf.evalue / :exchange_rate) + 0.5;
                ELSE
                    tf.avalue = :tf.evalue;
                ENDIF;
            ENDIF;

            /*----------------------------------------------------------
            If an extra question has been added to a euro form
            (avalue = 0 and evalue <> 0), but the evalue has NOT been
            changed by the user (rowstate = 2), calculate the avalue.
            ----------------------------------------------------------*/

            IF :tf.avalue = 0 AND :tf.evalue <> 0 AND MOD(:tf.question,10) = 1
            THEN
                tf.avalue = (:tf.evalue / :exchange_rate) + 0.5;
            ENDIF;

        };
    ENDIF;

    /*------------------------------------------------------------------
     Delete entries on bto_scanning_errors
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        DELETE FROM bto_scanning_errors
        WHERE       contributor_reference = :contributor_reference
        AND         period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in deleting from bto_scanning_errors'
        WITH STYLE = POPUP;
        RETURN 9;

    ENDIF;

    /*------------------------------------------------------------------
    Add 1 to the f9_count
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE contributor_period
	SET    f9_count = f9_count + 1
        WHERE  contributor_reference = :contributor_reference
	AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error with f9_count updating contributor_period'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows with f9_count updating contributor_period'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
    If any previously confirmed values have been changed, un-confirm
    them so they are checked again.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf(h_rowstate = _state, h_record = _record)
    {
        IF h_rowstate = 3 AND
        tf.confirmation_code <> '' AND tf.confirmation_code <> 'R'
        THEN
            tf.confirmation_code = '';
        ENDIF;
    };

    ret_val = CALLPROC lp_check_credibility ();
    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    REDISPLAY;

    /*------------------------------------------------------------------
     If h_data_vetted = "y", the form has passed all data vet and
     credibility tests (or has had all incredible values confirmed)
     and is automatically taken on. If h_data_vetted = "n", the User is
     given the option of taking the form on as it is, or having the form
     redisplayed so that any amendments may be made.
    ------------------------------------------------------------------*/

    IF h_data_vetted = 'y'
    THEN
        h_dummy_present = 'n';

        UNLOADTABLE tf
        {
            IF tf.acell = 'W'
            THEN
                h_dummy_present = 'y';
            ENDIF;
        };

    ELSEIF h_data_vetted = 'n'
    THEN
        h_yesno = '';
        WHILE h_yesno <> 'n' AND h_yesno <> 'y'
        DO
            h_yesno =  PROMPT 'Form has failed to clear. ' +
                            'Do you still wish to take it on? ' +
                            'Type Y/N and press <return>'
            WITH STYLE = POPUP;
            h_yesno = LOWERCASE(h_yesno);
        ENDWHILE;

        IF h_yesno = 'n'
        THEN
            RESUME;
        ENDIF;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
       RETURN ret_val;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed and :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        DELETE FROM cqpv
        WHERE       contributor_reference = :contributor_reference
        AND         quest_order = 9
        AND         question IN (SELECT question FROM :temp_del_aos)
        AND         period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
       RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'Unable to delete uncoded questions from cqpv: ' +
                'form not taken on'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    /*------------------------------------------------------------------
     Create temp table with deleted uncoded entries data.
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        CREATE TABLE :h_temp_del_table (
		     contributor_reference     CHAR(11) NOT NULL,
		     contributor_industry      INTEGER4 NOT NULL,
		     question                  INTEGER4 NOT NULL,
		     period                    INTEGER4 NOT NULL,
		     avalue                    INTEGER4 NOT NULL,
		     user_id                   VARCHAR(10) NOT NULL,
		     removed_date              DATE NOT NULL);

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error creating ' + :h_temp_del_table
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Insert deleted and updated uncoded entries into temp table.
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;


        INSERT INTO  :h_temp_del_table(
		     contributor_reference,
		     contributor_industry,
		     question,
		     period,
		     avalue,
		     user_id,
		     removed_date)
	SELECT       :contributor_reference,
		     :contributor_industry,
		     question,
		     :period,
		     avalue,
		     :g_user_id,
		     'now'
        FROM         :temp_del_aos;
        

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error inserting into ' + :h_temp_del_table
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Insert deleted and updated uncoded entries into removed
     uncoded entries table.
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        INSERT INTO removed_uncoded_entries
	SELECT * FROM :h_temp_del_table;
        

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error inserting removed questions'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
     Empty temp_del_aos.
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        MODIFY :temp_del_aos TO truncated;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error modifying table'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
    Update cqpv with form's values
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        UPDATE cqpv a
        FROM   :temp_cqpv b
        SET    acell                   = b.acell,
               avalue                  = b.avalue,
               acell_type              = b.acell_type,
               aconstruction_type      = b.aconstruction_type,
               confirmation_code       = b.confirmation_code,
               atypical                = b.atypical
               /*atypical                = :h_atypical*/
        WHERE  a.contributor_inquiry   = :inquiry
        AND    a.contributor_reference = :contributor_reference
        AND    a.question              = b.question
        AND    a.quest_order           = b.quest_order
        AND    a.period                = :period
        AND    a.contributor_industry  = :contributor_industry
        AND    b.update_type           = 'u';

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9 OR ret_val = 1
    THEN
        MESSAGE 'Form questions were not updated on cqpv'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
    Update cqpv with Euro form's values.
    The "where :currency = 'E'" ensures that this only happens for
    Euro forms (where E=E).
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        UPDATE cqpv a
        FROM   :temp_cqpv b
        SET    evalue                  = b.evalue
        WHERE  a.contributor_inquiry   = :inquiry
        AND    a.contributor_reference = :contributor_reference
        AND    a.question              = b.question
        AND    a.quest_order           = b.quest_order
        AND    a.period                = :period
        AND    a.contributor_industry  = :contributor_industry
        AND    b.update_type           = 'u'
        AND    :currency               = 'E'
        AND    (MOD(a.question,10) = 1 OR MOD(a.question,10000)=9200);

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'Euro Form questions were not updated on cqpv'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    /*------------------------------------------------------------------
    If there are any new questions added to the form, insert into cqpv
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        SELECT COUNT(question) AS h_insert_count
        FROM   :temp_cqpv
        WHERE  update_type = 'i';

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error counting questions'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    IF h_insert_count > 0
    THEN
        ret_val = CALLPROC msab_fp_ins_added (
                  currency              = :currency,
                  contributor_reference = :contributor_reference,
                  period                = :period,
                  inquiry               = :inquiry,
                  contributor_industry  = :contributor_industry,
                  temp_cqpv             = :temp_cqpv);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*------------------------------------------------------------------
    Update/Insert atq links for any quarterly questions on annual forms
    ------------------------------------------------------------------*/

    IF :inquiry = 14
    THEN
        ret_val = CALLPROC msab_fp_insert_atq (
                  temp_cqpv = :temp_cqpv,
                  period    = :period,
                  h_dummy   = :h_dummy,
                  h_idbr    = :h_idbr);

        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    /*------------------------------------------------------------------
    Store the first-time taken-on values
    ------------------------------------------------------------------*/

    ret_val = CALLPROC msab_fp_store_first (
              inquiry = :inquiry,
              period  = :period,
              h_dummy = :h_dummy,
              h_idbr  = :h_idbr);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    h_questions_changed = 'n';

    IF :h_dummy_present = 'y'
    THEN
        MESSAGE 'Take on completed but uncoded entries held'
        WITH STYLE = POPUP;
    ELSE
        /*--------------------------------------------------------------
         delete any existing reclassification proposals
        --------------------------------------------------------------*/

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            DELETE FROM reclass_contrib
            WHERE       contributor_reference = :contributor_reference
            AND         period                = :period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck = 1,
                      h_rollback   = 'N',
                      h_commit     = 'Y',
                      h_retries    = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name  = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error deleting from reclass_contrib'
            WITH STYLE = POPUP;
            RETURN 9;
        ENDIF;

        /*--------------------------------------------------------------
          Reclassify
        --------------------------------------------------------------*/

        IF :h_data_vetted = 'y'
        THEN
            MESSAGE 'Reclassification Test Starts . . .';

            ret_val = CALLPROC msab_fp_reclass_topdown (
                      contributor_reference = :contributor_reference,
                      period                = :period,
                      contributor_industry  = :contributor_industry,
                      alt_industry          = :alt_industry,
                      contributor_inquiry   = :inquiry,
                      h_data_vetted         = BYREF(:h_data_vetted));

            MESSAGE 'Reclassification Test Ends . . .';

            IF ret_val <> 0
            THEN
                RETURN ret_val;
            ENDIF;
        ENDIF;
    ENDIF;

    /*------------------------------------------------------------------
    Values have been updated on cqpv, so update them on
    contributor_period too
    ------------------------------------------------------------------*/

    ret_val = CALLPROC msab_fp_update_conper (
              contributor_reference = :contributor_reference,
              h_dummy_present       = :h_dummy_present,
              form_taken_on         = :form_taken_on,
              inquiry               = :inquiry,
              period                = :period);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    ret_val = CALLPROC msa_fp_drop_table (
              h_table = :h_temp_del_table);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    /*------------------------------------------------------------------
       Ask users to make an entry on the contact pad.
    ------------------------------------------------------------------*/

    h_yesno = '';
    WHILE h_yesno <> 'y' AND h_yesno <> 'n'
    DO
        h_yesno = PROMPT 'Have you amended current or back data? ' +
			 'Please make entry on Contact Pad. ' +
			 'Do you wish to enter Contact Pad now? ' +
 			 ' Type Y/N and press <return>'
	     	  WITH STYLE = POPUP;
        h_yesno = LOWERCASE(:h_yesno);
    ENDWHILE;

    IF h_yesno = 'y'
    THEN
	CALL APPLICATION(exec = '$IMAGES/communicate',
			 param = VARCHAR(:contributor_reference));
    ENDIF;

    /**** Repaced by other contact pad popup * 
    IF :h_confirm = 'y'
    THEN
        MESSAGE 'Have you updated the contact pad?'
        WITH STYLE = POPUP;
    ENDIF;
    ****/

    ret_val = CALLFRAME msab_fr_clear_form ();

    IF ret_val = 0
    THEN
        RETURN 0;
    ENDIF;

    /*------------------------------------------------------------------
    'Return to Form' has been pressed:
    The form has been cleared and taken onto cqpv, so change any
    "inserted" update-types to "updated"
    ------------------------------------------------------------------*/

    h_figures_changed = 'n';
    h_counter = 0;

    UNLOADTABLE tf
    {
        tf.update_type = 'u';
        h_counter = h_counter + 1;
        tf.h_col = h_counter;
    };

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        UPDATE :temp_cqpv
        SET    update_type = 'u';

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in final update for temp_cqpv'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;
}

'ContactDetails' =
{
    msa_date = 'now';
    CALL APPLICATION(exec = '$IMAGES/communicate',
         param = VARCHAR(:contributor_reference));
}

'History', KEY FRSKEY4 =
{
    msa_date = 'now';
 /*   IF :tf.quest_order = 10 OR :tf.quest_order = 11*/
    IF :tf.quest_order = 10
    THEN
        /*--------------------------------------------------------------
         Standard headings with SPH
        --------------------------------------------------------------*/

        ret_val = CALLFRAME msab_fr_std_hdg_hist_sph (
                  question  = :tf.question,
                  period    = :period,
                  h_dummy   = :h_dummy,
                  h_idbr    = :h_idbr);

        IF ret_val <> 0
        THEN
            MESSAGE 'Error calling standard headings sph history table'
            WITH STYLE = POPUP;
            RETURN ret_val;
        ENDIF;

        RESUME;
    ENDIF;

    IF :tf.quest_order <> 1
    THEN
        /*--------------------------------------------------------------
         Standard headings
        --------------------------------------------------------------*/

        ret_val = CALLFRAME msab_fr_std_hdg_hist (
                  question  = :tf.question,
                  period    = :period,
                  h_dummy   = :h_dummy,
                  h_idbr    = :h_idbr);

        IF ret_val <> 0
        THEN
            MESSAGE 'Error calling standard headings sph history table'
            WITH STYLE = POPUP;
            RETURN ret_val;
        ENDIF;

        RESUME;

    ENDIF;

    IF MOD(tf.question,10) = 5 OR MOD(tf.question,10) = 6
    THEN
        /*--------------------------------------------------------------
         05 or 06 - no UVs required
        --------------------------------------------------------------*/

        ret_val = CALLFRAME msab_fr_quest_hist0 (
                  period   = :period,
                  question = :tf.question,
                  h_dummy  = :h_dummy,
                  h_idbr   = :h_idbr);

        IF ret_val <> 0
        THEN
            MESSAGE 'Error calling quest0 history table'
            WITH STYLE = POPUP;
            RETURN ret_val;
        ENDIF;

    ELSEIF MOD(tf.question,10) = 4
    THEN
        /*--------------------------------------------------------------
         04 selected - check 02 exists
        --------------------------------------------------------------*/

        vol_question2 = :tf.question - 2;

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO
            h_retries = :h_retries + 1;

            SELECT question AS vol_question2
            FROM   :temp_cqpv
            WHERE  question = :vol_question2;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck = 0,
                      h_rollback   = 'N',
                      h_commit     = 'Y',
                      h_retries    = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name  = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error accessing volume 02'
            WITH STYLE = POPUP ;
            RETURN 9;
        ELSEIF ret_val = 1
        THEN
            /*----------------------------------------------------------
             04 selected and 02 exists
            ----------------------------------------------------------*/

            ret_val = CALLFRAME msab_fr_quest_hist2 (
                      question = :tf.question,
                      period   = :period,
                      h_dummy  = :h_dummy,
                      h_idbr   = :h_idbr);

            IF ret_val <> 0
            THEN
                MESSAGE 'Error calling quest2 history table'
                WITH STYLE = POPUP;
                RETURN ret_val;
            ENDIF;

        ELSEIF ret_val = 0
        THEN
            /*----------------------------------------------------------
             04 only exists
            ----------------------------------------------------------*/

            ret_val = CALLFRAME msab_fr_quest_hist1 (
                      period   = :period,
                      question = :tf.question,
                      h_dummy  = :h_dummy,
                      h_idbr   = :h_idbr);

            IF ret_val <> 0
            THEN
                MESSAGE 'Error calling quest1 history table'
                WITH STYLE = POPUP;
                RETURN ret_val;
            ENDIF;

        ENDIF;

    ELSEIF MOD(tf.question,10) = 2
    THEN
        /*--------------------------------------------------------------
         02 selected - check 04 exists
        --------------------------------------------------------------*/

        vol_question4 = :tf.question + 2;

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO
            h_retries = :h_retries + 1;

            SELECT question AS vol_question4
            FROM   :temp_cqpv
            WHERE  question = :vol_question4;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck = 0,
                      h_rollback   = 'N',
                      h_commit     = 'Y',
                      h_retries    = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name  = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error accessing volume 04'
            WITH STYLE = POPUP ;
            RETURN 9;
        ELSEIF ret_val = 1
        THEN
            /*----------------------------------------------------------
             02 selected and 04 exists
            ----------------------------------------------------------*/

            ret_val = CALLFRAME msab_fr_quest_hist2 (
                      period   = :period,
                      question = :tf.question,
                      h_dummy  = :h_dummy,
                      h_idbr   = :h_idbr);

            IF ret_val <> 0
            THEN
                MESSAGE 'Error calling quest2 history table'
                WITH STYLE = POPUP;
                RETURN ret_val;
            ENDIF;

        ELSEIF ret_val = 0
        THEN
            /*----------------------------------------------------------
             02 only exists
            ----------------------------------------------------------*/

            ret_val = CALLFRAME msab_fr_quest_hist1 (
                      period   = :period,
                      question = :tf.question,
                      h_dummy  = :h_dummy,
                      h_idbr   = :h_idbr);

            IF ret_val <> 0
            THEN
                MESSAGE 'Error calling quest1 history table'
                WITH STYLE = POPUP;
                RETURN ret_val;
            ENDIF;
        ENDIF;
    ELSE
        /*--------------------------------------------------------------
         01 selected - check 02 exists
        --------------------------------------------------------------*/

        vol_question2 = :tf.question + 1;
        vol_question4 = :tf.question + 3;

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            SELECT question AS vol_question2
            FROM   :temp_cqpv
            WHERE  question = :vol_question2;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck = 1,
                      h_rollback   = 'N',
                      h_commit     = 'Y',
                      h_retries    = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name  = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error selecting volume2 question'
            WITH STYLE = POPUP ;
            RETURN 9;
        ELSEIF ret_val = 1
        THEN
            /*----------------------------------------------------------
             01 Selected - no 02 held
            ----------------------------------------------------------*/

            ret_val = CALLFRAME msab_fr_quest_hist0 (
                      period   = :period,
                      question = :tf.question,
                      h_dummy  = :h_dummy,
                      h_idbr   = :h_idbr);

            IF ret_val <> 0
            THEN
                MESSAGE 'Error calling quest0 history table'
                WITH style = popup;
                RETURN ret_val;
            ENDIF;

        /*--------------------------------------------------------------
         01 selected - check 04 exists
        --------------------------------------------------------------*/

        ELSEIF ret_val = 0
        THEN
            h_retry_or_not      = 'RETRY';
            h_retries           = 0;

            WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
            DO
                h_retries = :h_retries + 1;

                SELECT question AS vol_question4
                FROM   :temp_cqpv
                WHERE  question = :vol_question4;

                ret_val = CALLPROC check_inq_ing (
                          h_num_row_ck = 1,
                          h_rollback   = 'N',
                          h_commit     = 'Y',
                          h_retries    = :h_retries,
                          h_retries_allowed = :h_retries_allowed,
                          h_frpr_name  = :h_frpr_name);


                IF ret_val <> 7
                THEN
                   h_retry_or_not = '';
                ENDIF;

            ENDWHILE;

            IF ret_val = 7
            THEN
                RETURN 7;
            ELSEIF ret_val = 9
            THEN
                MESSAGE 'System error selecting volume 04'
                WITH STYLE = POPUP ;
                RETURN 9;
            ELSEIF ret_val = 1
            THEN
                /*------------------------------------------------------
                 01 with 02 only
                ------------------------------------------------------*/

                ret_val = CALLFRAME msab_fr_quest_hist1 (
                          period   = :period,
                          question = :tf.question,
                          h_dummy  = :h_dummy,
                          h_idbr   = :h_idbr);

                IF ret_val <> 0
                THEN
                    MESSAGE 'Error calling quest1 history table'
                    WITH STYLE = POPUP;
                    RETURN ret_val;
                ENDIF;

            ELSEIF ret_val = 0
            THEN
                /*------------------------------------------------------
                 01 selected and 02 and 04 exist
                ------------------------------------------------------*/

                ret_val = CALLFRAME msab_fr_quest_hist2 (
                          period   = :period,
                          question = :tf.question,
                          h_dummy  = :h_dummy,
                          h_idbr   = :h_idbr);

                IF ret_val <> 0
                THEN
                    MESSAGE 'Error calling quest2 history table'
                    WITH STYLE = POPUP;
                    RETURN ret_val;
                ENDIF;

            ENDIF;
        ENDIF;
    ENDIF;
}

'UpdateContributor', KEY FRSKEY3 =
{
    msa_date = 'now';
    CALL APPLICATION (
           exec = '$IMAGES/online_cont_sel_app msafd_fr_up_cont',
           param = VARCHAR(:contributor_reference));
}

'Gates' =
{
    msa_date = 'now';
    ret_val = CALLFRAME msab_fr_view_gates (
              question = :tf.question);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;
}

'Cell/Type' =
{
    msa_date = 'now';
    /*------------------------------------------------------------------
    Screen giving all cell code / type definitions.
    ------------------------------------------------------------------*/

    ret_val = CALLFRAME msab_fr_cell_codes ();
}

'EuroTotals' =
{
    msa_date = 'now';
    IF currency <> 'E'
    THEN
        MESSAGE 'This is only for Euro Forms.' + X'0D0D' +
                'It would display the total of the 01 Questions,' +
                ' the Total Sales Question, and any difference' +
                ' between them - in Euros.'
        WITH STYLE = POPUP;
        RESUME;
    ENDIF;

    euro_t_sales = 0;
    euro_t_of_01 = 0;

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10000) = 9200
        THEN
            euro_t_sales = tf.evalue;

        ELSEIF MOD(tf.question,10) = 1
        THEN
            euro_t_of_01 = euro_t_of_01 + tf.evalue;
        ENDIF;
    };

    MESSAGE 'Euro Total 01s   = ' + VARCHAR(euro_t_of_01) + X'0D' +
            'Euro Total Sales = ' + VARCHAR(euro_t_sales) + X'0D' +
            'Difference       = ' + VARCHAR(euro_t_of_01 - euro_t_sales)
    WITH STYLE = POPUP;
}

'ICR' =
{
    msa_date = 'now';

    /*------------------------------------------------------------------
    Display/amend ICR error codes
    ------------------------------------------------------------------*/

    ret_val = CALLFRAME msab_fr_icr_codes (
              contributor_reference = :contributor_reference,
              contributor_industry  = :contributor_industry,
              inquiry               = :inquiry,
              period                = :period);
}

'Key' =
{
    msa_date = 'now';
    ret_val = CALLFRAME msab_fr_key_responder (
              h_idbr  = :h_idbr,
              h_dummy = :h_dummy,
              period  = :period);

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;
}

'MPI' =
{
    msa_date = 'now';

    /*------------------------------------------------------------------
    Display MPI turnover values  
    ------------------------------------------------------------------*/

    ret_val = CALLFRAME msab_fr_mpi_congruence (
              contributor_reference = :contributor_reference,
              contributor_industry  = :contributor_industry,
              period                = :period);
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');
}

'End', KEY FRSKEY6  =
{
    msa_date = 'now';

    /*------------------------------------------------------------------
                                End
    ------------------------------------------------------------------*/

    UNLOADTABLE tf(h_rowstate=_state)
    {
        IF h_rowstate <> 2 OR h_questions_changed = 'y' OR
           h_figures_changed = 'y'
        THEN
            h_yesno = '';
            WHILE h_yesno <> 'n' AND h_yesno <> 'y'
            DO
                h_yesno = PROMPT 'You have updated the form since it' +
                                 ' was last taken on - do you wish' +
                                 ' to exit without taking on the' +
                                 ' changes? Type Y/N and press <return>'
                          WITH STYLE = POPUP;
                h_yesno = LOWERCASE(h_yesno);
            ENDWHILE;
            IF h_yesno = 'y'
            THEN
		IF :h_confirm = 'y'
		THEN
                   MESSAGE 'Have you updated the contact pad?'
                   WITH STYLE = POPUP;
		ENDIF;
                RETURN 0;
            ELSE
                RESUME;
            ENDIF;
        ENDIF;
    };

		IF :h_confirm = 'y'
		THEN
                   MESSAGE 'Have you updated the contact pad?'
                   WITH STYLE = POPUP;
		ENDIF;
    RETURN 0;
}

'QuickView', KEY FRSKEY7 =
{
    CALL APPLICATION (exec = '$IMAGES/quickview_app',
                     param = 'Take ');
}

/*----------------------------------------------------------------------
                L O C A L    P R O C E D U R E    1

Updates the temporary table :temp_cqpv with the contents of the screen
table field tf
----------------------------------------------------------------------*/

PROCEDURE lp_update_temp_cqpv =
{


    UNLOADTABLE tf(h_rowstate = _state)
    {
        IF h_rowstate <> 2
        THEN
            h_figures_changed = 'y';
            h_retry_or_not      = 'RETRY';
            h_retries           = 0;

            WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
            DO

                h_retries = :h_retries + 1;

                UPDATE :temp_cqpv
                SET    acell              = :tf.acell,
                       avalue             = :tf.avalue,
                       evalue             = :tf.evalue,
                       acell_type         = :tf.acell_type,
                       aconstruction_type = :tf.aconstruction_type,
                       confirmation_code  = :tf.confirmation_code,
                       error_msg          = :tf.error_msg
                       /*atypical           = :h_atypical*/
                WHERE  question           = :tf.question;

                ret_val = CALLPROC check_inq_ing (
                          h_num_row_ck = 1,
                          h_rollback   = 'N',
                          h_commit     = 'Y',
                          h_retries    = :h_retries,
                          h_retries_allowed = :h_retries_allowed,
                          h_frpr_name  = :h_frpr_name);

                IF ret_val <> 7
                THEN
                    h_retry_or_not = '';
                ENDIF;

            ENDWHILE;

            IF ret_val = 7
            THEN
                RETURN 7;
            ELSEIF ret_val = 9
            THEN
                MESSAGE 'System error updating temporary table with'+
                        ' question = ' + VARCHAR(:tf.question)
                WITH STYLE = POPUP;
                RETURN 9;
            ELSEIF ret_val = 1
            THEN
                MESSAGE 'Error - no rows updated when updating'+
                        ' temporary table with question ' +
                         VARCHAR(:tf.question)
                WITH STYLE = POPUP;
                RETURN 1;
            ENDIF;
        ENDIF;
    };
}

/*----------------------------------------------------------------------
                L O C A L    P R O C E D U R E    2

Loads screen table field tf from the temporary table :temp_cqpv
----------------------------------------------------------------------*/
/****
Here put message to display to users that this question used to be from a qtrly industry
******/

PROCEDURE lp_load_tf =
{


    CLEAR FIELD tf;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO
        h_retries = :h_retries + 1;

        tf = SELECT     question,
                        avalue,
                        evalue,
                        acell,
                        confirmation_code,
                        error_msg,
                        rounded,
                        acell_type,
                        aconstruction_type,
                        update_type,
                        quest_order AS quest_order,
                        question_industry,
                        question_inquiry

             FROM       :temp_cqpv
             ORDER BY   quest_order,
                        question;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error re-loading questions'
        WITH STYLE = POPUP;
        RETURN 9;
    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Error re-loading questions - no questions found'
        WITH STYLE = POPUP;
        RETURN 1;
    ENDIF;

    /*------------------------------------------------------------------
    The following code stores the number of rows on the table
    tf. This is used later to prevent the 'out of data' message
    appearing when the top or bottom of the table is reached; the code
    must be repeated whenever the number of rows is changed?
    ------------------------------------------------------------------*/

    h_counter = 0;
    UNLOADTABLE tf
    {
        h_counter = h_counter + 1;
        tf.h_col = h_counter;
    };

    IF :h_counter < :h_counter1
    THEN
        SCROLL tf TO :h_counter;
    ELSE
        SCROLL tf TO :h_counter1;
    ENDIF;
}

 
/*----------------------------------------------------------------------
                L O C A L    P R O C E D U R E    3

Checks credibility of questions
----------------------------------------------------------------------*/

PROCEDURE lp_check_credibility () =
{
    /*------------------------------------------------------------------
     If it's a Euro form:
       Add up the Euro 01s, and if they match the Euro 9200,
       then set the sterling 9200 to the sum of the sterling 01s.
     If you don't, you get
       Euro 01s = Euro 9200 but sterling 01s <> sterling 9200
       because each 01 would have been converted & rounded to sterling,
       and the sum of the rounded 01s may not match the 9200 figure.
    ------------------------------------------------------------------*/

    IF currency = 'E'
    THEN
        total_sales = 0;
        total_of_01 = 0;

        UNLOADTABLE tf
        {
            IF MOD(tf.question,10000) = 9200
            THEN
                total_sales = tf.evalue;

            ELSEIF MOD(tf.question,10) = 1
            THEN
                total_of_01 = total_of_01 + tf.evalue;
            ENDIF;
        };

        IF total_of_01 = total_sales
        THEN
            total_of_01 = 0;

            UNLOADTABLE tf
            {
                IF MOD(tf.question,10) = 1
                THEN
                    total_of_01 = total_of_01 + tf.avalue;
                ENDIF;
            };

            UNLOADTABLE tf
            {
                IF MOD(tf.question,10000) = 9200
                THEN
                    tf.avalue = total_of_01;
                ENDIF;
            };
        ENDIF;
    ENDIF;

    /*------------------------------------------------------------------
     Sets total_sales to value of total sales code 9200, and
     total_of_01 to the total of all 01 codes.
    ------------------------------------------------------------------*/

    total_sales = 0;
    total_of_01 = 0;
    difference = 0;

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10000) = 9200
        THEN
            total_sales = tf.avalue;

        ELSEIF MOD(tf.question,10) = 1
        THEN
            total_of_01 = total_of_01 + tf.avalue;
        ENDIF;
    };

    difference = ABS(total_of_01 - total_sales);

    /*------------------------------------------------------------------
    If a value has been changed to non-zero while it has a B cell code,
    remove the B
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF tf.avalue <> 0 AND tf.acell = 'B'
        THEN
            tf.acell = 'V';
        ENDIF;
    };

    /*------------------------------------------------------------------
    If a value has been changed to Zero while its volume has a B for
    batch construction code, reset the batch code
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10) = 1
        THEN
            store_val = tf.avalue;
        ENDIF;

        IF (MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4)
        AND (tf.acell = 'A' or tf.acell = 'B') AND store_val = 0
      /*  AND tf.acell = 'B' AND store_val = 0*/
        THEN
            tf.acell = 'Z';
        ENDIF;
    };

    /*------------------------------------------------------------------
    If a 02 or 04 has been changed to non-Zero while its 05, 08 or 06
    volume has a batch construction code, reset the batch code
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10) = 2
        THEN
            vol_question2 = tf.avalue;
            acell_question2 = tf.acell;

        ELSEIF MOD(tf.question,10) = 4
        THEN
            vol_question4 = tf.avalue;
            acell_question4 = tf.acell;
        ENDIF;

        IF (MOD(tf.question,10) = 5 OR MOD(tf.question,10) = 8)
        AND (tf.acell = 'A' or tf.acell = 'B') AND store_val = 0
        AND (vol_question2 <> 0 OR acell_question2 <> 'B')
        THEN
            tf.acell = 'Z';
        ENDIF;

        IF MOD(tf.question,10) = 6
        AND (tf.acell = 'A' or tf.acell = 'B') AND store_val = 0
        AND (vol_question4 <> 0 OR acell_question4 <> 'B')
        THEN
            tf.acell = 'Z';
        ENDIF;
    };

    /*------------------------------------------------------------------
    Highlight volumes when there is a value-volume mismatch
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF tf.acell = 'L' OR tf.acell = 'M'
        THEN
            tf.acell = '';
            tf.error_msg = '';
        ENDIF;
    };

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10) = 1
        THEN
            store_01  = tf.question;
            store_val = tf.avalue;
        ENDIF;

        /*--------------------------------------------------------------
        Zero Value & non-Zero Volume
        --------------------------------------------------------------*/

        IF  (MOD(tf.question,10) = 2 
             OR MOD(tf.question,10) = 4
              OR MOD(tf.question,10) = 5
               OR MOD(tf.question,10) = 6
                OR MOD(tf.question,10) = 8)
        AND tf.avalue > 0 AND store_val = 0 AND tf.acell <> 'W'
        THEN
            tf.acell = 'L';
            tf.acell_type = '0';
            tf.error_msg = 'Zero Value & Non-Zero Volume';
            tf.confirmation_code = '';
        ENDIF;

        /*--------------------------------------------------------------
        Zero Volume & non-Zero Value
        --------------------------------------------------------------*/

        IF  (MOD(tf.question,10) = 2 OR MOD(tf.question,10) = 4
	      OR MOD(tf.question,10) = 5 OR MOD(tf.question,10) = 6 
	       OR MOD(tf.question,10) = 8 OR MOD(tf.question,10) = 9)

        AND tf.avalue = 0 AND store_val > 0
        AND tf.acell <> 'B' AND tf.acell <> 'W'

        AND tf.confirmation_code <> 'V'

        THEN
            tf.acell = 'M';
            tf.acell_type = '0';
            tf.error_msg = 'Zero Volume & Non-Zero Value';
            tf.confirmation_code = '';
        ENDIF;

    };

    REDISPLAY;

    /*------------------------------------------------------------------
    get the register_emp
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT register_emp
        FROM   contributor_period
        WHERE  contributor_reference = :contributor_reference
        AND    period = :period;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck = 1,
                  h_rollback   = 'N',
                  h_commit     = 'Y',
                  h_retries    = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name  = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;
    ENDIF;

    IF ret_val = 9 OR ret_val = 1
    THEN
        MESSAGE 'Unable to find Register Employment on ' +
                ' contributor_period'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;
/*
    UNLOADTABLE tf
    {
    
        IF tf.avalue = 0 AND tf.question = h_employment AND total_sales <> 0
        THEN
            tf.avalue = :register_emp;
            tf.evalue = :register_emp;
            tf.confirmation_code = 'R';

            MESSAGE 'No Employment value has been entered,' +
                    ' but Total Sales is greater than zero' +
                    ' so Register Employment has been used instead.'
            WITH STYLE = POPUP;

        ENDIF;
    };
*/

    /*------------------------------------------------------------------
     Check if question has replaced more than one question this year.  
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        h_retry_or_not      = 'RETRY';
        h_retries           = 0;

        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO

            h_retries = :h_retries + 1;

            SELECT new_question AS h_new_question
            FROM   question_history
            WHERE  new_question = :tf.question
            AND    rep_type = 3
	    AND    period = :h_annual_period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);

            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;

        ENDWHILE;

        IF ret_val = 7
        THEN
            RETURN 7;

        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in accessing question_history'
            WITH STYLE = POPUP;
            RETURN 9;

        ELSEIF ret_val = 0
        THEN
            data_cont = 'LINK';
            qlink = 'Q';
            SET_FORMS FIELD '' (INVISIBLE(data_cont) = 0);
            SET_FORMS FIELD '' (INVISIBLE(qlink) = 0);
        ENDIF;
    };

    /*------------------------------------------------------------------
    Do Credibility Checking
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
        IF MOD(tf.question,10) = 1
        THEN
            h_value_of_01 = :tf.avalue;
        ENDIF;

        IF tf.acell <> 'W' AND tf.acell <> 'M' AND tf.acell <> 'L'
        AND tf.acell <> 'B' AND tf.acell <> 'A' AND tf.acell <> 'C'
        AND (tf.confirmation_code = '' OR tf.confirmation_code = 'R')
        THEN
            ret_val = CALLPROC msab_fp_credibility (
                      h_idbr        = :h_idbr,
                      h_dummy       = :h_dummy,
                      inquiry       = :inquiry,
                      period        = :period,
                      prev_period   = :prev_period,
                      question      = :tf.question,
                      quest_order   = :tf.quest_order,
                      avalue        = :tf.avalue,
                      register_emp  = :register_emp,
                      value_of_01   = :h_value_of_01,
                      total_of_01   = :total_of_01,
                      total_sales   = :total_sales,

		      h_new_total_sales = BYREF(:h_new_total_sales),
		      h_atypical        = BYREF(:h_atypical),
                      acell             = BYREF(:tf.acell),
                      acell_type        = BYREF(:tf.acell_type),
                      error_msg         = BYREF(:tf.error_msg));

            IF ret_val <> 0
            THEN
                RETURN ret_val;
            ENDIF;

            IF tf.error_msg <> ''
            THEN
                h_data_vetted = 'n';
            ENDIF;

            IF MOD(tf.question,10000) = 9200 AND h_atypical = 'y'
            THEN
                tf.avalue           = :h_new_total_sales;
                h_save_total_sales  = :h_new_total_sales;

                h_retry_or_not      = 'RETRY';
                h_retries           = 0;

                WHILE :h_retries < :h_retries_allowed AND :h_retry_or_not = 'RETRY'
                DO

                    h_retries = :h_retries + 1;

                    UPDATE :temp_cqpv
                    SET    atypical = 'Y'
		    WHERE  mod(question,10000) = 9200;

                    ret_val = CALLPROC check_inq_ing (
                              h_num_row_ck = 1,
                              h_rollback   = 'N',
                              h_commit     = 'Y',
                              h_retries    = :h_retries,
                              h_retries_allowed = :h_retries_allowed,
                              h_frpr_name  = :h_frpr_name);
        
                    IF ret_val <> 7
                    THEN
                        h_retry_or_not = '';
                    ENDIF;
        
                ENDWHILE;
        
                IF ret_val = 7
                THEN
                    RETURN 7;
                ENDIF;
        
                IF ret_val = 9 OR ret_val = 1
                THEN
                    MESSAGE 'Unable to update atypical marker for ' +
                            ' for auto editing of total sales '
                    WITH STYLE = POPUP;
                ENDIF;
            ENDIF;
        ENDIF;
    };

    IF difference <> 0
    THEN
        SET_FORMS FIELD '' (INVISIBLE(total_of_01)=0,
                            INVISIBLE(total_sales)=0,
                            INVISIBLE(difference)=0);
    ELSE
        SET_FORMS FIELD '' (INVISIBLE(total_of_01)=1,
                            INVISIBLE(total_sales)=1,
                            INVISIBLE(difference)=1);
    ENDIF;

    /*------------------------------------------------------------------
    In case the total sales was confirmed and did not go through
    credibility checking, put the '1' flag in the cell code if the
    01s don't add up (and remove the '1' flag if they do).
    If auto editing has taken place in credibility program then
    h_new_total_sales will not equal Zero. If so then remove the '1'
    flag from the total sales question.
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
       IF tf.quest_order = 10
       THEN

           IF difference <> 0 AND LEFT(tf.acell,1) <> '1' 
	      AND h_save_total_sales = 0
           THEN
               tf.acell = '1' + tf.acell;
           ELSEIF difference <> 0 AND LEFT(tf.acell,1) = '1' 
	      AND h_save_total_sales > 0
           THEN
               tf.acell = RIGHT(tf.acell,1);
           ELSEIF difference = 0 AND LEFT(tf.acell,1) = '1' 
	       AND h_save_total_sales = 0 
           THEN
               tf.acell = RIGHT(tf.acell,1);
           ENDIF;
       ENDIF;
/*
       IF tf.quest_order = 10
       THEN
           IF difference <> 0 AND LEFT(tf.acell,1) <> '1'
           THEN
               tf.acell = '1' + tf.acell;
           ELSEIF difference = 0 AND LEFT(tf.acell,1) = '1'
           THEN
               tf.acell = RIGHT(tf.acell,1);
           ENDIF;
       ENDIF;
    */
    };

    /*------------------------------------------------------------------
    Highlight Uncoded Entries
    ------------------------------------------------------------------*/

    UNLOADTABLE tf
    {
       IF tf.acell = 'W'
       THEN
           tf.error_msg = 'Uncoded Entry';
       ENDIF;
    };


    h_data_vetted = 'y';
    UNLOADTABLE tf
    {
        IF tf.error_msg <> '' OR tf.acell LIKE '1%'
        THEN
            h_data_vetted = 'n';
        ENDIF;
    };

    ret_val = CALLPROC lp_update_temp_cqpv ();
    IF ret_val <> 0
    THEN
        RETURN 9;
    ENDIF;

    ret_val = CALLPROC lp_load_tf ();
    IF ret_val <> 0
    THEN
        RETURN 9;
    ENDIF;

    RETURN 0;
}
