
/*----------------------------------------------------------------------
New Disclosure Letter - July 1999
Used for (1) Batch Disclosure letters ($form_type = "F")
	 (2) Batch Disclosure reminders ($form_type = "M")
         (3) Online reprints ($form_type = "R")
----------------------------------------------------------------------*/

.NAME msar_rw_disclosure

.DECLARE counter = i4,
         prev_question = i4,
         inq_text = varchar(45),
         hline    = c73,
         vline    = c73,
         dotline = c150,
         dashline = c100

.QUERY  SELECT a.contributor_reference,
               a.question,
               a.need_count,
               a.unit_1,
               a.quest_order,
               a.text_line1,
               a.text_line2,
               a.text_line3,
               a.text_line4,
               a.text_line5,
               a.text_line6,
               a.text_line7,
               a.text_line8,
               a.text_line9,
               a.text_line10,
               c.name1,
               c.ext1,
               a.contributor_industry,
               a.legal_status,
               a.region,
               b.addressee,
               b.add_name1,
               b.add_name2,
               b.add_name3,
               b.add_tradstyle1,
               b.add_tradstyle2,
               b.add_tradstyle3,
               b.add_line1,
               b.add_line2,
               b.add_line3,
               b.add_line4,
               b.add_line5,
               b.add_postcode
      FROM     $rep_table a,
               contributor b,
               $analyst_table c
      WHERE    a.contributor_reference = b.contributor_reference
      AND      a.period = $period
      AND      a.contributor_industry = c.industry
      ORDER BY contributor_industry,
               contributor_reference,
               quest_order,
               question

.LET counter = 0

.LET hline = PAD(' ________________________') +
             PAD('_________________________') +
             PAD('______________________ ')

.LET vline = PAD('|                        ') +
             PAD('                         ') +
             PAD('                      |')

.LET dotline = PAD('. . . . . . . . . . . . .') +
               PAD(' . . . . . . . . . . . . ') +
               PAD('. . . . . . . . . . . . .') +
               PAD(' . . . . . . . . . . . . ') +
               PAD('. . . . . . . . . . . . .') +
               PAD(' . . . . . . . . . . . . ')

.LET dashline = PAD('_ _ _ _ _ _ _ _ _ _ _ _ _') +
                PAD(' _ _ _ _ _ _ _ _ _ _ _ _ ') +
                PAD('_ _ _ _ _ _ _ _ _ _ _ _ _') +
                PAD(' _ _ _ _ _ _ _ _ _ _ _ _ ')

.LET inq_text = 'PRODCOM Survey'

.BREAK contributor_reference
.PAGELENGTH 90
.PAGEWIDTH 200

.HEADER contributor_reference
.LET prev_question = 0

/*-s--------------------------------------------------------------------
Call up Page 1:
Different JDES:
  1. NI Reminder
  2. GB Reminder
  3. NI Letter
  4. GB Letter
--------------------------------------------------------------------e-*/

.IF $form_type = 'M'
.THEN
    .IF region = 'YY'
    .THEN
         .PR '%%DJDE JDL=SPRD18,JDE=NIDR99,END;'
    .ELSE
         .PR '%%DJDE JDL=SPRD18,JDE=GBDR99,END;'
    .ENDIF
.ELSE
    .IF region = 'YY'
    .THEN
         .PR '%%DJDE JDL=SPRD18,JDE=NID99,END;'
    .ELSE
         .PR '%%DJDE JDL=SPRD18,JDE=GBD99,END;'
    .ENDIF
.ENDIF
.NL

/*-s--------------------------------------------------------------------
Page 1: 1st logical Page: Counter & Address.
Don't print the counter if it's a Reprint
--------------------------------------------------------------------e-*/
.LET counter = counter + 1

.IF $form_type <> 'R'
.THEN
    .T 000 .PR '21' .T 003 .PR counter("nnnn") .NL
.ENDIF

.IF addressee <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR addressee .NL
.ENDIF

.IF add_name1 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_name1 .NL
.ENDIF

.IF add_name2 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_name2 .NL
.ENDIF

.IF add_name3 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_name3 .NL
.ENDIF

.IF legal_status = 1 OR legal_status = 2 OR legal_status = 3
OR legal_status = 4 OR legal_status = 7
.THEN
    .IF add_tradstyle1 <> ''
    .THEN
        .T 000 .PR '21' .T 003 .PR 'T/A ' add_tradstyle1 .NL
    .ENDIF

    .IF add_tradstyle2 <> ''
    .THEN
        .T 000 .PR '21' .T 003 .PR add_tradstyle2 .NL
    .ENDIF

    .IF add_tradstyle3 <> ''
    .THEN
        .T 000 .PR '21' .T 003 .PR add_tradstyle3 .NL
    .ENDIF
.ENDIF

.IF add_line1 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_line1 .NL
.ENDIF

.IF add_line2 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_line2 .NL
.ENDIF

.IF add_line3 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_line3 .NL
.ENDIF

.IF add_line4 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_line4 .NL
.ENDIF

.IF add_line5 <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_line5 .NL
.ENDIF

.IF add_postcode <> ''
.THEN
    .T 000 .PR '21' .T 003 .PR add_postcode .NL
.ENDIF

/*-s--------------------------------------------------------------------
Page 1: 2nd logical Page: GB Name/Ref/Date
        3rd logical Page: NI Name/Ref/Date
--------------------------------------------------------------------e-*/
.NP

.IF region <> 'YY'
.THEN
    .T 000 .PR '42' .T 003 .PR 'Your contact for help and queries:' .NL
    .T 000 .PR '42' .T 003 .PR name1 .NL
    .T 000 .PR '42' .T 003 .PR 'Telephone: 01633 45' ext1 .NL
    .T 000 .PR '42' .T 003 .PR 'Fax: 01633 652025' .NL 2
    .T 000 .PR '41'
    .T 003 .PR 'Please quote: '
              contributor_industry(f5) '  '
              TRIM(LEFT(contributor_reference,4)) '  '
              RIGHT(TRIM(LEFT(contributor_reference,8)),4) '  '
              TRIM(RIGHT(contributor_reference,3)) .NL 2
    .T 000 .PR '41'
    .T 003 .PR $despatch_date(d"3rd February 1901")
    .NP
.ELSE
    .NP
    .T 000 .PR '42' .T 003 .PR 'Your contact for help and queries:' .NL
    .T 000 .PR '42' .T 003 .PR 'Janet Wright' .NL
    .T 000 .PR '42' .T 003 .PR '' .NL 2
    .T 000 .PR '41'
    .T 003 .PR 'Please quote: '
              contributor_industry(f5) '  '
              TRIM(LEFT(contributor_reference,4)) '  '
              RIGHT(TRIM(LEFT(contributor_reference,8)),4) '  '
              TRIM(RIGHT(contributor_reference,3)) .NL 2
    .T 000 .PR '41'
    .T 003 .PR $despatch_date(d"3rd February 1901")
.ENDIF

/*-s--------------------------------------------------------------------
Page 1: 4th logical Page: NI Heading
        5th logical Page: GB Heading
--------------------------------------------------------------------e-*/
.NP
.IF region <> 'YY'
.THEN
    .NP
.ENDIF

.T 000 .PR '82' .T 003
.IF $form_type = 'M'
.THEN
    .PR 'Reminder - PRODCOM Survey - Permission to Publish'
.ENDIF

.IF region = 'YY' AND $form_type != 'M'
.THEN
    .PR 'PRODCOM Survey - Permission to Publish'
    .NP
.ENDIF

/*-s--------------------------------------------------------------------
End of Page 1 (Side B of Page 1 is blank).
Now call Page 2 and all subsequent pages which are FORM 14T15B
--------------------------------------------------------------------e-*/
.NL
.PR '%%DJDE FORMS=14T15B,BEGIN=(0.57,0.55),SIDE=NUFRONT,END;'
.NP
.T 000 .PR '41' .T 100 .PR 'Our Ref: ' contributor_industry(f5) '/'
                         contributor_reference(c11) '/' $period(f6)

/*-s--------------------------------------------------------------------
Section A: Permission
--------------------------------------------------------------------e-*/
.NL 2

.T 000 .PR '82' .T 003 .PR 'A: Permission' .NL 2
.T 000 .PR '41' .T 003
.PR 'I hereby give permission, for the Office for National Statistics'
    ' to publish any totals of which our figures may form the' .NL
.T 000 .PR '41' .T 003
.PR 'whole or a significant part relating to the ' inq_text ' for '
.IF $inquiry = 15
.THEN
    .PR 'the four quarters of '
.ENDIF
.PR $period/100(f4) '.' .NL 4

.T 000 .PR '41' .T 003 .PR 'Signed ' dotline(c96)
                         ' Date ' dotline(c52) .NL 3
.T 000 .PR '41' .T 003 .PR 'Position in Company ' dotline(c136) .NL 3
.T 000 .PR '41' .T 003 .PR 'Name of Company  ' dotline(c138) .NL 3
.T 000 .PR '62' .T 003 .PR dashline(c99) .NL 4

/*-s--------------------------------------------------------------------
Section B: Specific Permission
--------------------------------------------------------------------e-*/
.T 000 .PR '82' .T 003 .PR 'B: Specific Permission' .NL 2
.T 000 .PR '52' .T 003
.PR '(A complete listing of the products involved is printed below.'
    ' If you complete this section, please cross out'
.NL .T 000 .PR '52' .T 003
.PR 'the products that you do not give your'
    ' permission to publish).'
.NL 2

.T 000 .PR '41' .T 003
.PR 'I hereby give permission, for the Office for National Statistics'
    ' to publish the following product totals relating to the' .NL
.T 000 .PR '41' .T 003
.PR inq_text ' for '
.IF $inquiry = 15
.THEN
    .PR 'the four quarters of '
.ENDIF
.PR $period/100(f4) '.'
.NL 2

/*-s--------------------------------------------------------------------
Now start printing the questions
--------------------------------------------------------------------e-*/

.DETAIL

.IF need_count <> 0
.THEN
    .IF prev_question <> 0
    .THEN
         .T 000 .PR '61' .T 003 .PR vline .NL
    .ENDIF

    .T 000 .PR '61' .T 003 .PR hline
     /* .T 005 .PR '>' line_number('nnn') '-' need_count('nnn') '<' */
    .IF prev_question <> 0
    .THEN
         .T 003 .PR vline
    .ENDIF
    .NL

    .IF line_number + need_count > 70
    .THEN
        .PR '%%DJDE FORMS=14T15B,BEGIN=(0.57,0.55),END;'
        .NP
        .T 000 .PR '41' .T 100
              .PR 'Our Ref: ' contributor_industry(f5) '/'
                              contributor_reference(c11) '/' $period(f6)
        .NL 2
        .T 000 .PR '61' .T 003 .PR hline .NL
    .ENDIF
    .T 000 .PR '61' .T 003 .PR vline .NL
    .T 000 .PR '61' .T 003 .PR vline .NL

    .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line1(c65) .NL
    .IF text_line2 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line2(c65) .NL
    .ENDIF
    .IF text_line3 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line3(c65) .NL
    .ENDIF
    .IF text_line4 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line4(c65) .NL
    .ENDIF
    .IF text_line5 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line5(c65) .NL
    .ENDIF
    .IF text_line6 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line6(c65) .NL
    .ENDIF
    .IF text_line7 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line7(c65) .NL
    .ENDIF
    .IF text_line8 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line8(c65) .NL
    .ENDIF
    .IF text_line9 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line9(c65) .NL
    .ENDIF
    .IF text_line10 <> ''
    .THEN
        .T 000 .PR '61' .T 003 .PR vline .T 007 .PR text_line10(c65) .NL
    .ENDIF
    .T 000 .PR '61' .T 003 .PR vline .NL
.ENDIF
.T 000 .PR '61' .T 003 .PR vline .NL
.T 000 .PR '61' .T 003 .PR vline .T 007 .PR question(f9) '  '
.IF MOD(question,10) < 2
.THEN
    .PR 'Value in '
.ELSE
    .PR 'Volume in '
.ENDIF
.PR unit_1 .NL
.LET prev_question = question

/*-s--------------------------------------------------------------------
After all the specific questions have been printed at the end of
Section B, print the signature, etc.
--------------------------------------------------------------------e-*/
.FOOTER contributor_reference
.T 000 .PR '61' .T 003 .PR vline .NL
.T 000 .PR '61' .T 003 .PR hline
               .T 003 .PR vline
/* .T 005 .PR '>' line_number('nnn') '<' */
.NL

.IF line_number > 70 - 13
.THEN
    .PR '%%DJDE FORMS=14T15B,BEGIN=(0.57,0.55),END;'
    .NP
    .T 000 .PR '41' .T 100
          .PR 'Our Ref: ' contributor_industry(f5) '/'
                          contributor_reference(c11) '/' $period(f6)
    .NL 2
.ENDIF

.NL 3
.T 000 .PR '41' .T 003 .PR 'Signed ' dotline(c96)
                         ' Date ' dotline(c52) .NL 3
.T 000 .PR '41' .T 003 .PR 'Position in Company ' dotline(c136) .NL 3
.T 000 .PR '41' .T 003 .PR 'Name of Company  ' dotline(c138) .NL 3
.T 000 .PR '62' .T 003 .PR dashline(c99)
/* .T 003 .PR '>' line_number('nnn') '<' */

/*-s--------------------------------------------------------------------
Section C, Refusal. (Always starts on a new sheet of paper).
--------------------------------------------------------------------e-*/
.NL
.PR '%%DJDE FORMS=14T15B,BEGIN=(0.57,0.55),SIDE=NUFRONT,END;'
.NP
.T 000 .PR '41' .T 100
      .PR 'Our Ref: ' contributor_industry(f5) '/'
                      contributor_reference(c11) '/' $period(f6)
.NL 6

.T 000 .PR '82' .T 003 .PR 'C: Refusal' .NL 2
.T 000 .PR '41' .T 003
.PR 'I do not give permission, for the Office for National Statistics'
    ' to publish any totals where there is any risk of disclosing' .NL
.T 000 .PR '41' .T 003
.PR 'information contributed by us to the '
    inq_text ' for '
.IF $inquiry = 15
.THEN
    .PR 'the four quarters of '
.ENDIF
.PR $period/100(f4) '.' .NL 4

.T 000 .PR '41' .T 003 .PR 'Signed ' dotline(c96)
                         ' Date ' dotline(c52) .NL 3
.T 000 .PR '41' .T 003 .PR 'Position in Company ' dotline(c136) .NL 3
.T 000 .PR '41' .T 003 .PR 'Name of Company  ' dotline(c138) .NL

/*-s--------------------------------------------------------------------
Final JDE to staple the form
--------------------------------------------------------------------e-*/
.FOOTER REPORT
.NL
.PR '%%DJDE JDL=STUNIX,JDE=POR1,END;'
