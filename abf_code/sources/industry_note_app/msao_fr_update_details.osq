/*----------------------------------------------------------------------
msao_fr_update_details
----------------------------------------------------------------------*/

INITIALIZE ( h_industry        = INTEGER4 NOT NULL,
             h_tid             = INTEGER4 NOT NULL) =

   DECLARE   h_reply           = CHAR(1) NOT NULL,
             h_line_no         = INTEGER2 NOT NULL,
             h_industry_text   = CHAR(76) NOT NULL,
             h_frpr_name       = VARCHAR(25) NOT NULL,
             h_retries_allowed = INTEGER1 NOT NULL,
             h_retries         = INTEGER1 NOT NULL,
             ret_val           = INTEGER1 NOT NULL,
             h_retry_or_not    = CHAR(5)  NOT NULL,
             contact_array     = ARRAY OF TYPE OF FORM msao_fo_update_details,
             lp_save_line      = PROCEDURE RETURNING INTEGER

{
    h_frpr_name = 'msao_fr_update_details';
    h_retries_allowed = 3;

    industry = :h_industry;  
    industry_title := industry_title;

    /*------------------------------------------------------------------
     Get initial details from main table
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT IFNULL(inform_date,' ') AS inform_date,
               IFNULL(inform_message,' ') AS inform_message
        FROM   industry_note_pad
        WHERE  industry_tid = :h_tid
        AND    industry = :h_industry;
                                
        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_frpr_name       = :h_frpr_name,
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9 
    THEN
        MESSAGE 'System error in loading from industry_note_pad'
        WITH STYLE = POPUP;
        RETURN 9;     

    ELSEIF ret_val = 1 
    THEN
        MESSAGE 'No initial details could be found'
        WITH STYLE = POPUP;
        RETURN 1;     
    ENDIF;

    /*------------------------------------------------------------------
     Get rest of details from extra table
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        SELECT line_no AS h_line_no,
               industry_text AS h_industry_text
        FROM   industry_extra_info
        WHERE  industry_tid = :h_tid
        AND    industry = :h_industry
        { 
            IF :h_line_no = 1
            THEN
                line1 := :h_industry_text;
            ELSEIF :h_line_no = 2
            THEN
                line2 := :h_industry_text;
            ELSEIF :h_line_no = 3
            THEN
                line3 := :h_industry_text;
            ELSEIF :h_line_no = 4
            THEN
                line4 := :h_industry_text;
            ELSEIF :h_line_no = 5
            THEN
                line5 := :h_industry_text;
            ELSEIF :h_line_no = 6
            THEN
                line6 := :h_industry_text;
            ELSEIF :h_line_no = 7
            THEN
                line7 := :h_industry_text;
            ELSEIF :h_line_no = 8
            THEN
                line8 := :h_industry_text;
            ELSEIF :h_line_no = 9
            THEN
                line9 := :h_industry_text;
            ELSEIF :h_line_no = 10
            THEN
                line10 := :h_industry_text;
            ELSEIF :h_line_no = 11
            THEN
                line11 := :h_industry_text;
            ELSEIF :h_line_no = 12
            THEN
                line12 := :h_industry_text;
            ELSEIF :h_line_no = 13
            THEN
                line13 := :h_industry_text;
            ELSEIF :h_line_no = 14
            THEN
                line14 := :h_industry_text;
            ELSEIF :h_line_no = 15
            THEN
                line15 := :h_industry_text;
            ELSEIF :h_line_no = 16
            THEN
                line16 := :h_industry_text;
            ELSEIF :h_line_no = 17
            THEN
                line17 := :h_industry_text;
            ELSEIF :h_line_no = 18
            THEN
                line18 := :h_industry_text;
            ENDIF;
        };
 
        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_frpr_name       = :h_frpr_name,
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9 
    THEN
        MESSAGE 'System error in loading industry_extra_info'
        WITH STYLE = POPUP;
        RETURN 9;     
    ENDIF;
    
    /*------------------------------------------------------------------
      Save the initial details for comparison purposes
    ------------------------------------------------------------------*/

    contact_array[1].inform_date := :inform_date;
    contact_array[1].inform_message := inform_message;
    contact_array[1].line1 := :line1;
    contact_array[1].line2 := :line2;
    contact_array[1].line3 := :line3;
    contact_array[1].line4 := :line4;
    contact_array[1].line5 := :line5;
    contact_array[1].line6 := :line6;
    contact_array[1].line7 := :line7;
    contact_array[1].line8 := :line8;
    contact_array[1].line9 := :line9;
    contact_array[1].line10 := :line10;
    contact_array[1].line11 := :line11;
    contact_array[1].line12 := :line12;
    contact_array[1].line13 := :line13;
    contact_array[1].line14 := :line14;
    contact_array[1].line15 := :line15;
    contact_array[1].line16 := :line16;
    contact_array[1].line17 := :line17;
    contact_array[1].line18 := :line18;

    /*------------------------------------------------------------------
      Position cursor on the field below the last written field
    ------------------------------------------------------------------*/

    IF :line18 <> '' THEN RESUME FIELD line18;
    ELSEIF :line17 <> '' THEN RESUME FIELD line18;
    ELSEIF :line16 <> '' THEN RESUME FIELD line17;
    ELSEIF :line15 <> '' THEN RESUME FIELD line16;
    ELSEIF :line14 <> '' THEN RESUME FIELD line15;
    ELSEIF :line13 <> '' THEN RESUME FIELD line14;
    ELSEIF :line12 <> '' THEN RESUME FIELD line13;
    ELSEIF :line11 <> '' THEN RESUME FIELD line12;
    ELSEIF :line10 <> '' THEN RESUME FIELD line11;
    ELSEIF :line9 <> '' THEN RESUME FIELD line10;
    ELSEIF :line8 <> '' THEN RESUME FIELD line9;
    ELSEIF :line7 <> '' THEN RESUME FIELD line8;
    ELSEIF :line6 <> '' THEN RESUME FIELD line7;
    ELSEIF :line5 <> '' THEN RESUME FIELD line6;
    ELSEIF :line4 <> '' THEN RESUME FIELD line5;
    ELSEIF :line3 <> '' THEN RESUME FIELD line4;
    ELSEIF :line2 <> '' THEN RESUME FIELD line3;
    ELSEIF :line1 <> '' THEN RESUME FIELD line2;
    ELSEIF :inform_message <> '' THEN RESUME FIELD line1;
    ELSE RESUME FIELD inform_message;
    ENDIF;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'Help',
               FILE = g_help + '/' + :h_frpr_name + '.hlp');
}

'ChangeDate', KEY FRSKEY4 =
{
    ret_val = CALLFRAME msao_fr_change_date (
              inform_date = BYREF(:inform_date));

    IF ret_val <> 0
    THEN
        MESSAGE 'Error calling up Change Date frame'
        WITH STYLE = POPUP;
    ENDIF;  
}

'Save', KEY FRSKEY11 =
{
    /*------------------------------------------------------------------
     Save initial details
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE industry_note_pad
        SET    inform_date    = :inform_date,
               inform_message = :inform_message
        WHERE  industry_tid     = :h_tid
        AND    industry = :h_industry;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_frpr_name       = :h_frpr_name,
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9 
    THEN
        MESSAGE 'System error in updating industry_note_pad'
        WITH STYLE = POPUP;
        RETURN 9;     

    ELSEIF ret_val = 1 
    THEN
        MESSAGE 'No rows saved for initial details'
        WITH STYLE = POPUP;
        RETURN 1;     
    ENDIF;

    /*------------------------------------------------------------------
     Delete extra details 
    ------------------------------------------------------------------*/

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        DELETE FROM industry_extra_info
        WHERE       industry_tid = :h_tid
        AND         industry = :h_industry;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_frpr_name       = :h_frpr_name,
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9 
    THEN
        MESSAGE 'System error deleting from industry_extra_info'
        WITH STYLE = POPUP;
        RETURN 9;     
    ENDIF;

    /*------------------------------------------------------------------
       Save the extra details                
    ------------------------------------------------------------------*/

    IF :line1 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 1,
                                            h_lp_text = :line1);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line2 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 2,
                                            h_lp_text = :line2);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line3 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 3,
                                            h_lp_text = :line3);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line4 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 4,
                                            h_lp_text = :line4);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line5 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 5,
                                            h_lp_text = :line5);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line6 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 6,
                                            h_lp_text = :line6);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line7 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 7,
                                            h_lp_text = :line7);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line8 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 8,
                                            h_lp_text = :line8);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line9 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 9,
                                            h_lp_text = :line9);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line10 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 10,
                                            h_lp_text = :line10);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line11 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 11,
                                            h_lp_text = :line11);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line12 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 12,
                                            h_lp_text = :line12);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line13 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 13,
                                            h_lp_text = :line13);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line14 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 14,
                                            h_lp_text = :line14);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line15 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 15,
                                            h_lp_text = :line15);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line16 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 16,
                                            h_lp_text = :line16);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line17 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 17,
                                            h_lp_text = :line17);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    IF :line18 <> ''
    THEN
        ret_val = CALLPROC lp_save_line ( h_lp_line = 18,
                                            h_lp_text = :line18);
        IF ret_val <> 0
        THEN
            RETURN ret_val;
        ENDIF;
    ENDIF;

    MESSAGE 'Industry details saved OK' WITH STYLE = POPUP;

    /*------------------------------------------------------------------
       Repopulate array with updated values
    ------------------------------------------------------------------*/

    contact_array[1].inform_date := :inform_date;
    contact_array[1].inform_message := inform_message;
    contact_array[1].line1 := :line1;
    contact_array[1].line2 := :line2;
    contact_array[1].line3 := :line3;
    contact_array[1].line4 := :line4;
    contact_array[1].line5 := :line5;
    contact_array[1].line6 := :line6;
    contact_array[1].line7 := :line7;
    contact_array[1].line8 := :line8;
    contact_array[1].line9 := :line9;
    contact_array[1].line10 := :line10;
    contact_array[1].line11 := :line11;
    contact_array[1].line12 := :line12;
    contact_array[1].line13 := :line13;
    contact_array[1].line14 := :line14;
    contact_array[1].line15 := :line15;
    contact_array[1].line16 := :line16;
    contact_array[1].line17 := :line17;
    contact_array[1].line18 := :line18;
}

'End', KEY FRSKEY14  =
{
    IF inform_date <> contact_array[1].inform_date
    OR inform_message <> contact_array[1].inform_message
    OR line1 <> contact_array[1].line1
    OR line2 <> contact_array[1].line2
    OR line3 <> contact_array[1].line3
    OR line4 <> contact_array[1].line4
    OR line5 <> contact_array[1].line5
    OR line6 <> contact_array[1].line6
    OR line7 <> contact_array[1].line7
    OR line8 <> contact_array[1].line8
    OR line9 <> contact_array[1].line9
    OR line10 <> contact_array[1].line10
    OR line11 <> contact_array[1].line11
    OR line12 <> contact_array[1].line12
    OR line13 <> contact_array[1].line13
    OR line14 <> contact_array[1].line14
    OR line15 <> contact_array[1].line15
    OR line16 <> contact_array[1].line16
    OR line17 <> contact_array[1].line17
    OR line18 <> contact_array[1].line18
    THEN
        h_reply := '';
        WHILE :h_reply <> 'y' AND :h_reply <> 'n'
        DO
            h_reply := PROMPT 'To save changes made, you must' +
                       ' select SAVE before exiting. Do you wish' +
                       ' to quit without saving ? Type Y/N and' +
                       ' press <return>' WITH STYLE = POPUP;
            h_reply = LOWERCASE(:h_reply);
        ENDWHILE;

        IF :h_reply = 'y'
        THEN
            RETURN 0;
        ENDIF; 

    ELSE
        RETURN 0;

    ENDIF; 
}

/*----------------------------------------------------------------------
                L O C A L    P R O C E D U R E S
----------------------------------------------------------------------*/

PROCEDURE lp_save_line ( h_lp_line = INTEGER2 NOT NULL,
                         h_lp_text = CHAR(76) NOT NULL) =
{
    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        INSERT INTO industry_extra_info (
                    industry,
                    industry_tid,
                    line_no,
                    industry_text)
        VALUES (    :h_industry,
                    :h_tid,
                    :h_lp_line,
                    :h_lp_text);    

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_frpr_name       = :h_frpr_name,
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_rollback        = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9 
    THEN
        MESSAGE 'System error in saving line ' + VARCHAR(:h_lp_line) 
        WITH STYLE = POPUP;
        RETURN 9;     

    ELSEIF ret_val = 1 
    THEN
        MESSAGE 'Error in saving line ' + VARCHAR(:h_lp_line) 
        WITH STYLE = POPUP;
        RETURN 1;     
    ENDIF;

    RETURN 0;

}
