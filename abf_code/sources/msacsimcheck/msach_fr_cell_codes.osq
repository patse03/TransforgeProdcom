/*----------------------------------------------------------------------
  Display cell codes
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed  = INTEGER1 NOT NULL,
        h_retries          = INTEGER1 NOT NULL,
        h_retry_or_not     = CHAR(5) NOT NULL,
        h_frpr_name        = VARCHAR(24) NOT NULL,
        tf.cell_code_order = INTEGER1 NOT NULL,
        ret_val            = INTEGER1 NOT NULL

{
    h_retries_allowed = 3;
    h_frpr_name = 'msach_fr_cell_codes';

    SET_FORMS FIELD '' (MODE(tf) = 'READ');

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        tf =

        SELECT   cell_code,
                 text,
                 cell_code_order
        FROM     cell_codes
        ORDER BY cell_code_order;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing cell codes'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No rows found on cell_codes table'
        WITH STYLE = POPUP;
        RETURN 1;
    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
