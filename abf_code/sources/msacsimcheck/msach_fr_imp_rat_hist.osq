/*----------------------------------------------------------------------
 History of Ratios 
----------------------------------------------------------------------*/

INITIALIZE () =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        start_period      = INTEGER4 NOT NULL,
        end_period        = INTEGER4 NOT NULL,
        ret_val           = INTEGER1 NOT NULL

{
    h_retries_allowed = 3;
    h_frpr_name = 'msach_fr_imp_rat_hist';
    SET_FORMS FIELD '' (MODE(tf) = 'READ');

    IF g_periods > 5
    THEN
        g_row = 5;
    ELSE
        g_row = g_periods - 1;
    ENDIF;

    start_period = :g_period[g_row].period;
    end_period = :g_period[1].period;

    MESSAGE 'Selecting Data . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        tf = 

        SELECT   period,
                 imputation_ratio
        FROM     question_period
        WHERE    inquiry = :g_inquiry
        AND      question_industry  = :g_industry
        AND      period BETWEEN :start_period AND :end_period
        AND      question = :question
        UNION
        SELECT   a.period,
                 a.imputation_ratio 
        FROM     question_period a,
                 question_history b
        WHERE    a.inquiry = :g_inquiry
        AND      a.question_industry  = :g_industry
        AND      a.period BETWEEN :start_period AND :end_period
        AND      b.new_question = :question
        AND      b.old_question = a.question
        AND      b.rep_type = 1
        ORDER BY period DESC;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing question_period/qh'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No imputation data held for Periods ' +
                VARCHAR(:start_period) + ' to ' +
                VARCHAR(:end_period)
        WITH STYLE = POPUP;
        RETURN 0;
    ENDIF;

}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
