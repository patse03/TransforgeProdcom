/*----------------------------------------------------------------------
 Prints NEW Contributor labels - 16 per page, print using -oPOR16 
 11 lines per label 
 (the printer issues another line feed between logical pages)
----------------------------------------------------------------------*/

.name msaaa_rw_batch_labels

.query

select   a.period,
         a.inquiry,
         a.contributor_reference,
         a.contributor_industry,
         a.region,
         c.add_name1,
         c.add_tradstyle1,
         c.add_line1,
         c.add_line2,
         c.add_line3,
         c.add_line4,
         c.add_line5,
         c.add_postcode
from     contributor_period a,
         contributor c
where    a.contributor_reference = c.contributor_reference
and      a.inquiry = $inquiry
and      a.period = $period
and      a.output_category in ('C','G')
order by contributor_industry,
         contributor_reference

.format contributor_reference  (c11)
        contributor_industry   (f5)
        period                 (f4)
        region                 (c2)
        add_name1              (c35)
        add_tradstyle1         (c35)
        add_line1              (c30)
        add_line2              (c30)
        add_line3              (c30)
        add_line4              (c30)
        add_line5              (c30)
        add_postcode           (c8)

.noformfeeds
.pagelength 11

.detail

  .nl
  .rightmargin 46
  .rt .pr contributor_industry '/' 
          contributor_reference '/' $inquiry(f2)
  .nl2 .t06 .pr add_name1
  .nl .t06  .pr add_line1
  .if add_line2 <> '' .then .nl .t06 .pr add_line2 .endif
  .if add_line3 <> '' .then .nl .t06 .pr add_line3 .endif
  .if add_line4 <> '' .then .nl .t06 .pr add_line4 .endif
  .if add_line5 <> '' .then .nl .t06 .pr add_line5 .endif
  .if add_postcode <> '' .then .nl .t06 .pr add_postcode .endif

  .if region = 'YY' .then .rt .pr 'NI' .endif
  .np
