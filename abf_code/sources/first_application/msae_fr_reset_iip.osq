/*------------------------------------------------------------------
                 Deletes a Username's IIP Entry

         Either called by itself or from msae_fr_kill_user
------------------------------------------------------------------*/

INITIALIZE (

        h_calling_proc  = VARCHAR(10) NOT NULL) =

DECLARE h_command       = VARCHAR(200) NOT NULL,
        h_yesno         = CHAR(1) NOT NULL,
        h_frpr_name     = VARCHAR(24) NOT NULL,
        ret_val         = INTEGER1 NOT NULL
{
    database = 'prodlivedb';
    msa_date = 'now';
    h_frpr_name = 'msae_fr_reset_iip';

    IF LOWERCASE(:h_calling_proc) = 'kill_user'
    THEN
        SET_FORMS FIELD '' (DISPLAYONLY(username) = 1,
                            REVERSE(username) = 1,
                            UNDERLINE(username) = 0);
        RESUME FIELD database;
    ELSE
        SET_FORMS FIELD '' (DISPLAYONLY(username) = 0,
                            REVERSE(username) = 0,
                            UNDERLINE(username) = 1);
        RESUME FIELD username;
    ENDIF;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'WhosIn' =
{
    h_command = 'whosin | pg';

    CALL SYSTEM :h_command;
    msa_date = 'now';
}

FIELD username =
{
    IF username = ''
    THEN
        RESUME FIELD username;
    ENDIF;

    IF LENGTH(username) < 7
    THEN
        MESSAGE 'Username should be at least 7 characters'
        WITH STYLE = POPUP;
        RESUME FIELD username;
    ENDIF;

    IF LEFT(username,6) <> 'proddc' AND
       LEFT(username,5) <> 'prod1'  AND
       LEFT(username,5) <> 'prod0'
    THEN
        MESSAGE 'Username should begin with ''proddc'', ''prod0'' or' +
                ' ''prod1'''
        WITH STYLE = POPUP;
        RESUME FIELD username;
    ENDIF;

    IF :g_user_id = :username
    THEN
        MESSAGE 'You cannot reset your own IIP entry'
        WITH STYLE = POPUP;
        RESUME FIELD username;
    ENDIF;

    RESUME FIELD database;
}

FIELD database =
{

    IF  (database <> 'prodlivedb')
    AND (database <> 'produserdb')
    THEN
        MESSAGE 'Only the databases ''prodlivedb''' +
                ' and ''produserdb'' are valid'
        WITH STYLE = POPUP;
        RESUME FIELD database;
    ENDIF;

    h_yesno = '';

    WHILE :h_yesno <> 'y' AND :h_yesno <> 'n'
    DO
         h_yesno := PROMPT 'Really reset ' + :username+ '''s IIP Entry'+
                           ' on ' + :database + 
                           '? Type Y/N and press <return>'
                    WITH STYLE = POPUP;
         h_yesno = LOWERCASE(h_yesno);
    ENDWHILE;

    IF h_yesno = 'y'
    THEN
        h_command := SQUEEZE('iip ' + :database + ' ' + :username +
                             '; echo; echo ; echo | pg');
        CALL SYSTEM :h_command;

        IF LOWERCASE(:h_calling_proc) = 'kill_user'
        THEN
            RETURN 0;
        ENDIF;

        msa_date = 'now';
        CLEAR FIELD username;
        RESUME FIELD username;

    ENDIF;
    msa_date = 'now';
}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}

'QuickView', KEY FRSKEY7 =
{
    CALL APPLICATION (exec = '$IMAGES/quickview_app -numeric_overflow=ignore ');
}
