PROCEDURE msa_fp_update_iip (

        h_exit            = CHAR(1) NOT NULL) =

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL,
        h_action          = CHAR(1) NOT NULL

{
    h_retries_allowed = 3;
    h_frpr_name = 'msa_fp_update_iip';

    h_action = UPPERCASE(:h_exit);

    IF :h_action <> 'Y' AND :h_action <> 'N'
    THEN
        MESSAGE 'msa_fp_update_iip: Exit Status should be "Y" or "N"'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        UPDATE inter_image_params
        SET    system_exit = :h_action
        WHERE  user_id = (SELECT user_name FROM iidbconstants);

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in updating inter_image_params'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'No entry was found on the iip table to be updated'
        WITH STYLE = POPUP;
        RETURN 1;
    ENDIF;

    RETURN 0;

}

