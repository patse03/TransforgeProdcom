/*----------------------------------------------------------------------
                  change_exp_password

This frame enables the user to change their password, if it has
expired. They must type in their old expired password, their new
password and then repeat it for confirmation.

TABLES ACCESSED: PASSWORDS
----------------------------------------------------------------------*/

INITIALIZE() =

DECLARE ret_val = INTEGER NOT NULL,
        h_attempts = INTEGER NOT NULL,
        h_comp_pass = CHAR(5) NOT NULL,
        h_user_id_2 = CHAR(10) NOT NULL,
        h_password = CHAR(5) NOT NULL,
        h_prompt = CHAR(1) NOT NULL,
        h_frpr_name = VARCHAR(24) NOT NULL,
        h_changed = CHAR(1) NOT NULL,
        h_retries = INTEGER NOT NULL,
        h_retries_allowed = INTEGER NOT NULL,
        h_retry_or_not = CHAR(5) NOT NULL

{
    h_frpr_name = 'change_exp_password';
    h_retries_allowed = 3;
    msa_date = 'now';
    h_changed = 'N';
    h_attempts = 0;
}


FIELD old_password =
{
    IF LENGTH(:old_password) <> 5
    THEN
         MESSAGE 'Your password MUST be 5 characters in length. ' +
                 'Please enter a correct length password'
         WITH STYLE = POPUP;
         CLEAR FIELD old_password;
         RESUME FIELD old_password;
    ENDIF;

    ret_val = CALLPROC msa_fp_get_user_details (
              h_password = BYREF(:h_password));

    IF ret_val <> 0
    THEN
        RETURN ret_val;
    ENDIF;

    IF old_password <> :h_password
    THEN
        MESSAGE 'You have not enter your existing password correctly. '+
                'Please try again'
        WITH STYLE = POPUP;
        h_attempts = :h_attempts + 1;
        CLEAR FIELD old_password;
        RESUME FIELD old_password;
    ENDIF;

    IF h_attempts = 3
    THEN
        MESSAGE 'After three attempts the system is exited '+
                'for security reasons'
        WITH STYLE = POPUP;
        RETURN 9;
    ENDIF;

    RESUME FIELD new_password;
}

FIELD 'new_password' =
{

    IF LENGTH(:new_password) <> 5
    THEN
        MESSAGE 'Your password must be 5 characters in length. ' +
                'Please enter a correct length password'
        WITH STYLE = POPUP;
        CLEAR FIELD new_password;
        RESUME FIELD new_password;
    ENDIF;

    IF new_password = :old_password
    THEN
        MESSAGE 'Your new password cannot be the same as your ' +
                'old password - please enter a different password'
        WITH STYLE = POPUP;
        CLEAR FIELD new_password;
        RESUME FIELD new_password;
    ENDIF;

    h_comp_pass = :new_password;

    ret_val = CALLFRAME msa_fr_ck_password(
              h_comp_pass = BYREF(:new_password));

    IF ret_val <> 0
    THEN
        RETURN 9;
    ENDIF;

    IF h_comp_pass <> :new_password
    THEN
        MESSAGE 'Confirmation of the new password has failed. ' +
                'Please re-enter your new password'
        WITH STYLE = POPUP;
        CLEAR FIELD new_password;
        msa_date = 'now';
        RESUME FIELD new_password;
    ENDIF;

    h_retry_or_not = 'RETRY';
    h_retries = 0;

    WHILE (:h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY')
    DO
        h_retries = :h_retries + 1;

        UPDATE passwords
        SET    password = :new_password,
               date_changed = 'today'
        WHERE  user_id = :g_user_id;

        ret_val = CALLPROC check_inq_ing(
                           h_num_row_ck = 1,
                           h_commit     = 'Y',
                           h_frpr_name  = :h_frpr_name,
                           h_retries    = :h_retries,
                           h_retries_allowed  = :h_retries_allowed,
                           h_rollback = 'N');

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'Error updating passwords - please contact IS'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        MESSAGE 'Error no rows updated in passwords - ' +
                'please contact IS'
        WITH STYLE = POPUP;
        RETURN 1;
    ENDIF;

    MESSAGE 'New password accepted' WITH STYLE = POPUP;

    h_changed = 'Y';
    h_attempts = 0;
    CLEAR FIELD ALL;
    RETURN 0;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'End', KEY FRSKEY6 =
{
    IF h_changed  = 'N'
    THEN
        h_prompt = '';
        WHILE h_prompt <> 'n' AND h_prompt <> 'y'
        DO
            h_prompt = PROMPT
            'You have selected END without completing the change of' +
            ' password. Do you wish to go back and complete ?' +
            ' Type Y/N and press <return>'
            WITH STYLE = POPUP;
            h_prompt = LOWERCASE(:h_prompt);

        ENDWHILE;

        IF h_prompt = 'y'
        THEN
            RESUME FIELD old_password;
        ENDIF;
    ENDIF;
    RETURN 0;
}
