INITIALIZE ()=

DECLARE h_retries_allowed = INTEGER1 NOT NULL,
        h_retries         = INTEGER1 NOT NULL,
        h_retry_or_not    = CHAR(5) NOT NULL,
        h_frpr_name       = VARCHAR(24) NOT NULL,
        ret_val           = INTEGER1 NOT NULL
{
    msa_date = 'now';
    h_retries_allowed = 3;
    h_frpr_name = 'msai_fr_in_all_reminders';

    SET_FORMS FIELD '' (MODE(tf) = 'READ',
                        REVERSE(period) = 0,
                        UNDERLINE(period) = 1,
                        DISPLAYONLY(period) = 0);
}

FIELD period =
{

    IF period <> 999999
    THEN

        h_retry_or_not      = 'RETRY';
        h_retries           = 0;
    
        WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
        DO
    
            h_retries = :h_retries + 1;
    
            SELECT period
            FROM   inquiry_period
            WHERE  period = :period;

            ret_val = CALLPROC check_inq_ing (
                      h_num_row_ck      = 1,
                      h_commit          = 'Y',
                      h_rollback        = 'N',
                      h_retries         = :h_retries,
                      h_retries_allowed = :h_retries_allowed,
                      h_frpr_name       = :h_frpr_name);
    
            IF ret_val <> 7
            THEN
                h_retry_or_not = '';
            ENDIF;
    
        ENDWHILE;
    
        IF ret_val = 7
        THEN
            RETURN 7;
    
        ELSEIF ret_val = 9
        THEN
            MESSAGE 'System error in accessing inquiry_period'
            WITH STYLE = POPUP;
            RETURN 9;
    
        ELSEIF ret_val = 1
        THEN
            MESSAGE 'Period does not exist'
            WITH STYLE = POPUP;
            RESUME FIELD period;
        ENDIF;
    ENDIF;

    MESSAGE 'Selecting Data . . .';

    h_retry_or_not      = 'RETRY';
    h_retries           = 0;

    WHILE :h_retries < h_retries_allowed AND :h_retry_or_not = 'RETRY'
    DO

        h_retries = :h_retries + 1;

        IF period = 999999
        THEN
            tf =

            SELECT   inquiry,
                     period,
                     reminder,
                     industry,
                     contributor_reference,
                     rem_type,
                     date_created
            FROM     stop_reminder
            ORDER BY industry,
                     contributor_reference,
                     reminder,
                     period;
        ELSE
            tf =

            SELECT   inquiry,
                     period,
                     reminder,
                     industry,
                     contributor_reference,
                     rem_type,
                     date_created
            FROM     stop_reminder
            WHERE    period = :period
            ORDER BY industry,
                     contributor_reference,
                     reminder;
        ENDIF;

        ret_val = CALLPROC check_inq_ing (
                  h_num_row_ck      = 1,
                  h_commit          = 'Y',
                  h_rollback        = 'N',
                  h_retries         = :h_retries,
                  h_retries_allowed = :h_retries_allowed,
                  h_frpr_name       = :h_frpr_name);

        IF ret_val <> 7
        THEN
            h_retry_or_not = '';
        ENDIF;

    ENDWHILE;

    IF ret_val = 7
    THEN
        RETURN 7;

    ELSEIF ret_val = 9
    THEN
        MESSAGE 'System error in accessing stop_reminder'
        WITH STYLE = POPUP;
        RETURN 9;

    ELSEIF ret_val = 1
    THEN
        IF :period = 999999
        THEN
            MESSAGE 'No Stop Reminders details are held.'
            WITH STYLE = POPUP;
        ELSE
            MESSAGE 'No Stop Reminders details are held for this' +
                    ' Period.'
            WITH STYLE = POPUP;
        ENDIF;

        RESUME FIELD period;

    ENDIF;

    SET_FORMS FIELD '' (REVERSE(period) = 1,
                        UNDERLINE(period) = 0,
                        DISPLAYONLY(period) = 1);

    RESUME FIELD tf;
}

'Help', KEY FRSKEY1 =
{
    HELP_FORMS(SUBJECT = 'HELP',
               FILE = :g_help + '/' + :h_frpr_name + '.hlp');

}

'Clear' =
{
    CLEAR FIELD period, tf;
    msa_date = 'now';

    SET_FORMS FIELD '' (REVERSE(period) = 0,
                        UNDERLINE(period) = 1,
                        DISPLAYONLY(period) = 0);
    RESUME FIELD period;
}

'End', KEY FRSKEY6 =
{
    RETURN 0;
}
